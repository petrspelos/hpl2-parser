
//-------------------------------------------------

//////////////////////////////
// EXTRAMODES AND CUSTOM CODE
//////////////////////////////

//-------------------------------------------------

string gsCurrentMapName = "00_rainy_hall";

string gsFunnyEasierEntry = "FunnyEasier";
string gsFunnyEasierSuffix = "_funny_easier";
string gsFunnyEasyEntry = "FunnyEasy";
string gsFunnyEasySuffix = "_funny_easy";
string gsFunnyTougherEntry = "FunnyTougher";
string gsFunnyTougherSuffix = "_funny_tougher";
string gsFunnyToughEntry = "FunnyTough";
string gsFunnyToughSuffix = "_funny_tough";
string gsFunnyEntry = "Funny";
string gsFunnySuffix = "_funny";
string gsStandardEasierEntry = "StandardEasier";
string gsStandardEasierSuffix = "_easier";
string gsStandardEasyEntry = "StandardEasy";
string gsStandardEasySuffix = "_easy";
string gsStandardTougherEntry = "StandardTougher";
string gsStandardTougherSuffix = "_tougher";
string gsStandardToughEntry = "StandardTough";
string gsStandardToughSuffix = "_tough";
string gsStandardEntry = "Standard";
string gsStandardSuffix = "";

string gsHealthPotion = "potion_health";
string gsLargeOilPotion = "potion_oil_large";
string gsOilPotion = "potion_oil";
string gsSanityPotion = "potion_sanity";
string gsTinderbox = "tinderbox";

int[] gviEasierTinderboxesCodesArray = {1, 2, 3, 4, 5, 6, 7, 8};
int[] gviEasierOilPotionsCodesArray = {0};
int[] gviEasierHealthPotionsCodesArray = {0};
int[] gviEasierSanityPotionsCodesArray = {0};
int[] gviEasierLargeOilPotionsCodesArray = {0};

int[] gviEasyTinderboxesCodesArray = gviEasierTinderboxesCodesArray;
int[] gviEasyOilPotionsCodesArray = gviEasierOilPotionsCodesArray;
int[] gviEasyHealthPotionsCodesArray = gviEasierHealthPotionsCodesArray;
int[] gviEasySanityPotionsCodesArray = gviEasierSanityPotionsCodesArray;
int[] gviEasyLargeOilPotionsCodesArray = gviEasierLargeOilPotionsCodesArray;

int[] gviNormalTinderboxesCodesArray = {1, 2, 3, 4, 5};
int[] gviNormalOilPotionsCodesArray = {0};
int[] gviNormalHealthPotionsCodesArray = {0};
int[] gviNormalSanityPotionsCodesArray = {0};
int[] gviNormalLargeOilPotionsCodesArray = {0};

int[] gviToughTinderboxesCodesArray = {7, 8};
int[] gviToughOilPotionsCodesArray = {0};
int[] gviToughHealthPotionsCodesArray = {0};
int[] gviToughSanityPotionsCodesArray = {0};
int[] gviToughLargeOilPotionsCodesArray = {0};

int[] gviTougherTinderboxesCodesArray = {8};
int[] gviTougherOilPotionsCodesArray = {0};
int[] gviTougherHealthPotionsCodesArray = {0};
int[] gviTougherSanityPotionsCodesArray = {0};
int[] gviTougherLargeOilPotionsCodesArray = {0};

int giMaxGlobalDeathCount = 9;

void PlayEnemySoundAtEntity(string asSound, string asEntity, string asType) //HELPER FUNCTION.
{
	PlaySoundAtEntity(asEntity + "_" + asSound, GetStringWithReplacedFunnySubString(asType + "_" + asSound, asType), asEntity, 0.0f, false);
}

//START STRING MANAGEMENT FUNCTIONS
int GetSubStringIndex(string asString, string asSubString, int aiOffset) //RETURNS THE STARTING POINT/INDEX OF A DEFINED SUBSTRING CONTAINED IN A STRING, IF THE DEFINED SUBSTRING COULD NOT BE FOUND IT RETURNS THE NEGATIVE VALUE -1. THE THIRD ARGUMENT DEFINES THE STARTING POINT/INDEX OF THE STRING TO START THE SEARCH.
{
	if(aiOffset >= asString.length() || aiOffset < 0)
	{
		return -1;
	}
	
	string sExaminedString = StringSub(asString, aiOffset, asString.length() - aiOffset);
	int iStringLength = sExaminedString.length();
	int iSubStringLength = asSubString.length();
	
	if(iStringLength == iSubStringLength)
	{
		if(sExaminedString == asSubString)
		{
			return 0;
		}
		else
		{
			return -1;
		}
	}
	else
	if(iSubStringLength > iStringLength)
	{
		return -1;
	}
	else
	{
		int iFirstMatchIndex = 0;
		int iLastMatchIndex = 0;
		int iMatchCount = 0;
		
		for(int i=0; i<iStringLength; i++)
		{
			if(sExaminedString[i] == asSubString[iMatchCount])
			{
				iLastMatchIndex = i;
				
				if(iMatchCount < iSubStringLength)
				{
					iMatchCount = iMatchCount + 1;
					
					if(iMatchCount == 1)
					{
						iFirstMatchIndex = i;
					}
					
					if(iMatchCount == iSubStringLength)
					{
						break;
					}
				}
			}
			else
			if(iMatchCount > 0)
			{
				iMatchCount = 0;
				
				if(asString[i] == asSubString[iMatchCount])
				{
					iLastMatchIndex = i;
					iMatchCount = iMatchCount + 1;
					iFirstMatchIndex = i;
				}
			}
		}
		
		if(iMatchCount == iSubStringLength)
		{
			return iFirstMatchIndex;
		}
		else
		{
			return -1;
		}
	}
}

string GetStringWithReplacedSubString(string asString, string asSearchedSubString, string asReplacedSubString) //RETURNS THE ORIGINAL STRING BUT IT HAS DEFINED SUBSTRINGS REPLACED, IF NO DEFINED SUBSTRINGS ARE FOUND THEN IT RETURNS JUST THE ORIGINAL STRING. FIRST ARGUMENT: ORIGINAL STRING, SECOND ARGUMENT: SUBSTRING THAT MUST BE CONTAINED INSIDE THE ORIGINAL STRING, THIRD ARGUMENT: STRING THAT WILL REPLACE ALL THE SECOND ARGUMENT SUBSTRINGS INSIDE THE ORIGINAL STRING.
{
	string sUnchangedFirstMatchSection = "";
	string sOriginalFirstMatchSection = "";
	string sReplacedFirstMatchSection = "";
	int iFirstMatchIndex = GetSubStringIndex(asString, asSearchedSubString, 0);
	
	if(iFirstMatchIndex == -1)
	{
		return asString;
	}
	else
	if(iFirstMatchIndex > 0)
	{
		sUnchangedFirstMatchSection = StringSub(asString, 0, iFirstMatchIndex);
	}
	
	sOriginalFirstMatchSection = sUnchangedFirstMatchSection + asSearchedSubString;
	sReplacedFirstMatchSection = sUnchangedFirstMatchSection + asReplacedSubString;
	
	if(sOriginalFirstMatchSection.length() < asString.length())
	{
		return sReplacedFirstMatchSection + GetStringWithReplacedSubString(StringSub(asString, sOriginalFirstMatchSection.length(), asString.length() - sOriginalFirstMatchSection.length()), asSearchedSubString, asReplacedSubString);
	}
	else
	{
		return sReplacedFirstMatchSection;
	}
}

string GetStringWithoutSubString(string asString, string asSubString) //RETURNS THE REMAINING STRING WITHOUT THE SUBSTRING DEFINED AS THE SECOND ARGUMENT.
{
	return GetStringWithReplacedSubString(asString, asSubString, "");
}

string GetSubString(string asString, int aiOffset) //RETURNS THE REMAINING SUBSTRING STARTING FROM THE OFFSET DEFINED AS THE SECOND ARGUMENT.
{
	int iLength = asString.length() - aiOffset;
	
	if(iLength <= 0)
	{
		return asString;
	}
	else
	{
		return StringSub(asString, aiOffset, iLength);
	}
}
//END STRING MANAGEMENT FUNCTIONS

void TimerTunePlayerLanternOil(string asTimer) //USED AS A TIMER TO CHECK AND ADJUST PLAYER LANTERN OIL VALUE.
{
	AddTimer(asTimer, 2.0f, "TimerTunePlayerLanternOil");
	
	if(asTimer == "oilspeedslow")
	{
		if(GetPlayerLampOil() < 99.0f)
		{
			AddPlayerLampOil(0.2f);
		}
	}
	else
	if(asTimer == "oilspeedquick")
	{
		if(GetPlayerLampOil() > 1.0f)
		{
			AddPlayerLampOil(-0.2f);
		}
	}
	else
	if(asTimer == "oilspeedoff")
	{
		SetPlayerLampOil(100.0f);
	}
}

void LanternLitOilSpeedSlow(bool abLit) //LANTERNLITCALLBACK FUNCTION. WHEN SET IT SLOWS DOWN PLAYER LANTERN OIL CONSUMPTION.
{
	if(abLit)
	{
		AddTimer("oilspeedslow", 2.0f, "TimerTunePlayerLanternOil");
	}
	else
	{
		RemoveTimer("oilspeedslow");
	}
}

void LanternLitOilSpeedQuick(bool abLit) //LANTERNLITCALLBACK FUNCTION. WHEN SET IT SPEEDS UP PLAYER LANTERN OIL CONSUMPTION.
{
	if(abLit)
	{
		AddTimer("oilspeedquick", 2.0f, "TimerTunePlayerLanternOil");
	}
	else
	{
		RemoveTimer("oilspeedquick");
	}
}

float GetDifficultyDirectFactor() //RETURNS THE VALUE OF THE DIRECT DIFFICULTY FACTOR.
{
	return GetGlobalVarFloat("DifficultyDirectFactor");
}

float GetDifficultyInverseFactor() //RETURNS THE VALUE OF THE INVERSE DIFFICULTY FACTOR.
{
	return GetGlobalVarFloat("DifficultyInverseFactor");
}

int GetEasyModeOn() //RETURNS 1 IF THE EASY MODE IS ENABLED, 2 IF THE EASIER MODE IS ENABLED, 0 ELSE.
{
	return GetGlobalVarInt("EasyModeOn");
}

string GetExtraModesSuffix() //RETURNS THE SUFFIX OF THE EXTRAMODES.
{
	int iChecker = (GetGlobalVarInt("FunnyModeOn") * 1) + (GetGlobalVarInt("EasyModeOn") * 2) + (GetGlobalVarInt("ToughModeOn") * 6);
	
	switch(iChecker)
	{
		case 1:
			return gsFunnySuffix;
		case 2:
			return gsStandardEasySuffix;
		case 3:
			return gsFunnyEasySuffix;
		case 4:
			return gsStandardEasierSuffix;
		case 5:
			return gsFunnyEasierSuffix;
		case 6:
			return gsStandardToughSuffix;
		case 7:
			return gsFunnyToughSuffix;
		case 12:
			return gsStandardTougherSuffix;
		case 13:
			return gsFunnyTougherSuffix;
		default:
			return gsStandardSuffix;
	}
	
	return "";
}

bool GetFunnyModeOn() //RETURNS TRUE IF THE FUNNY MODE IS ENABLED.
{
	return (GetGlobalVarInt("FunnyModeOn") >= 1);
}

string GetStringWithExtraModesSuffix(string asString) //RETURNS THE NAME + THE EXTRAMODES SUFFIX IF SOME EXTRAMODES ARE ACTIVE.
{
	return asString + GetExtraModesSuffix();
}

string GetStringWithReplacedFunnySubString(string asString, string asSearchedSubString) //RETURNS A MODIFIED VERSION OF THE STRING GIVEN AS THE FIRST ARGUMENT IF FUNNY MODE IS ACTIVE.
{
	if(GetFunnyModeOn())
	{
		if(StringContains(asString, asSearchedSubString))
		{
			return GetStringWithReplacedSubString(asString, asSearchedSubString, asSearchedSubString + gsFunnySuffix);
		}
		else
		{
			AddDebugMessage("ERROR: Could not find " + asSearchedSubString + " in " + asString + "! Could not replace it with " + asSearchedSubString + gsFunnySuffix + "!", false);
			return asString;
		}
	}
	else
	{
		return asString;
	}
}

int GetToughModeOn() //RETURNS 1 IF THE TOUGH MODE IS ENABLED, 2 IF THE TOUGHER MODE IS ENABLED, 0 ELSE.
{
	return GetGlobalVarInt("ToughModeOn");
}

void SetDifficultyDirectFactor(float afAmount) //CHANGES THE VALUE OF THE DIRECT DIFFICULTY FACTOR.
{
	SetGlobalVarFloat("DifficultyDirectFactor", afAmount);
}

void SetDifficultyInverseFactor(float afAmount) //CHANGES THE VALUE OF THE INVERSE DIFFICULTY FACTOR.
{
	SetGlobalVarFloat("DifficultyInverseFactor", afAmount);
}

void SetEasyModeOn(int aiState) //CHANGES THE VALUE OF THE EASY DIFFICULTY MODE. 0 -> off, 1 -> on, 2 -> easier mode on.
{
	if(aiState < 0)
	{
		aiState = 0;
	}
	else
	if(aiState > 2)
	{
		aiState = 2;
	}
	
	if(aiState > 0)
	{
		SetGlobalVarInt("ToughModeOn", 0);
	}
	
	SetGlobalVarInt("EasyModeOn", aiState);
}

void SetFunnyModeOn(bool abState) //CHANGES THE VALUE OF THE FUNNY MODE. false -> off, true -> on.
{
	if(abState)
	{
		SetGlobalVarInt("FunnyModeOn", 1);
	}
	else
	{
		SetGlobalVarInt("FunnyModeOn", 0);
	}
}

void SetToughModeOn(int aiState) //CHANGES THE VALUE OF THE TOUGH DIFFICULTY MODE. 0 -> off, 1 -> on, 2 -> tougher mode on.
{
	if(aiState < 0)
	{
		aiState = 0;
	}
	else
	if(aiState > 2)
	{
		aiState = 2;
	}
	
	if(aiState > 0)
	{
		SetGlobalVarInt("EasyModeOn", 0);
	}
	
	SetGlobalVarInt("ToughModeOn", aiState);
}

void GiveItemFromFile(string asItem) //GIVES AN ITEM THAT CAN BE EITHER USED AS A DEBUG ITEM OR AS A GOODY.
{
	string sItem = GetStringWithoutSubString(asItem, ".ent");
	int i = 0;
	
	do
	{
		i = i + 1;
	}
	while(HasItem(sItem + "_" + i));
	
	GiveItemFromFile(sItem + "_" + i, sItem + ".ent");
}

void TimerTunePlayerState(string asTimer)
{
	float fCooldown = 10.0f;
	float fHealth = GetPlayerHealth();
	float fOil = GetPlayerLampOil();
	float fSanity = GetPlayerSanity();
	int iRand = RandInt(0, 4);
	
	if(StringContains(asTimer, "Blessing"))
	{
		if(asTimer == "BlessingRegainHealth")
		{
			if(fHealth <= 99.0f)
			{
				SetPlayerHealth(fHealth + 1.0f);
			}
			else
			if(fHealth < 100.0f)
			{
				SetPlayerHealth(100.0f);
			}
			
			fCooldown = RandFloat(6.0f, 9.0f);
		}
		else
		if(asTimer == "BlessingRegainSanity")
		{
			if(fSanity <= 99.0f)
			{
				SetPlayerSanity(fSanity + 1.0f);
			}
			else
			if(fSanity < 100.0f)
			{
				SetPlayerSanity(100.0f);
			}
			
			fCooldown = RandFloat(6.0f, 9.0f);
		}
		else
		if(asTimer == "BlessingRegainLanternOil")
		{
			if(fOil <= 95.0f)
			{
				SetPlayerLampOil(fOil + 5.0f);
			}
			else
			if(fOil < 100.0f)
			{
				SetPlayerLampOil(100.0f);
			}
			
			fCooldown = 90.0f;
		}
		else
		if(asTimer == "BlessingRandomGoodies")
		{
			fCooldown = RandFloat(240.0f, 480.0f);
			
			if(iRand == 0)
			{
				GiveItemFromFile(gsTinderbox + ".ent");
				PlayGuiSound("pick_generic.snt", 0.5f);
			}
			else
			if(iRand == 1)
			{
				GiveItemFromFile(gsOilPotion + ".ent");
				PlayGuiSound("pick_potion.snt", 0.5f);
			}
			else
			if(iRand == 2)
			{
				GiveItemFromFile(gsHealthPotion + ".ent");
				PlayGuiSound("pick_potion.snt", 0.5f);
			}
			else
			if(iRand == 3)
			{
				GiveItemFromFile(gsSanityPotion + ".ent");
				PlayGuiSound("pick_potion.snt", 0.5f);
			}
		}
	}
	else
	if(StringContains(asTimer, "Curse"))
	{
		if(asTimer == "CurseRegainHealth")
		{
			if(fHealth < 50.0f && fHealth > 5.0f)
			{
				SetPlayerHealth(fHealth - 1.0f);
			}
			
			fCooldown = RandFloat(6.0f, 9.0f);
		}
		else
		if(asTimer == "CurseRegainSanity")
		{
			if(fSanity < 50.0f && fSanity > 5.0f)
			{
				SetPlayerSanity(fSanity - 0.4f);
			}
			
			fCooldown = RandFloat(6.0f, 9.0f);
		}
		else
		if(asTimer == "CurseRegainLanternOil")
		{
			if(fOil < 50.0f && fOil > 5.0f)
			{
				SetPlayerLampOil(fOil - 5.0f);
			}
			
			fCooldown = 90.0f;
		}
		else
		if(asTimer == "CurseRandomGoodies")
		{
			fCooldown = RandFloat(240.0f, 480.0f);
			int i = 0;
			
			if(iRand == 0)
			{
				do
				{
					i = i + 1;
				}
				while(HasItem(gsTinderbox + "_" + i) == false && i <= 20);
				
				if(HasItem(gsTinderbox + "_" + i))
				{
					RemoveItem(gsTinderbox + "_" + i);
					PlayGuiSound("impact_glass_low.snt", 0.5f);
				}
			}
			else
			if(iRand == 1)
			{
				do
				{
					i = i + 1;
				}
				while(HasItem(gsOilPotion + "_" + i) == false && i <= 20);
				
				if(HasItem(gsOilPotion + "_" + i))
				{
					RemoveItem(gsOilPotion + "_" + i);
					PlayGuiSound("impact_glass_low.snt", 0.5f);
				}
			}
			else
			if(iRand == 2)
			{
				do
				{
					i = i + 1;
				}
				while(HasItem(gsHealthPotion + "_" + i) == false && i <= 20);
				
				if(HasItem(gsHealthPotion + "_" + i))
				{
					RemoveItem(gsHealthPotion + "_" + i);
					PlayGuiSound("impact_glass_low.snt", 0.5f);
				}
			}
			else
			if(iRand == 3)
			{
				do
				{
					i = i + 1;
				}
				while(HasItem(gsSanityPotion + "_" + i) == false && i <= 20);
				
				if(HasItem(gsSanityPotion + "_" + i))
				{
					RemoveItem(gsSanityPotion + "_" + i);
					PlayGuiSound("impact_glass_low.snt", 0.5f);
				}
			}
		}
	}
	
	AddTimer(asTimer, fCooldown, "TimerTunePlayerState");
}

void SetupCurrentMapBasedOnExtraModes()
{
	string sPrefix = "";
	float fDifficultyDirectFactor = 0.0f;
	float fDifficultyInverseFactor = 0.0f;
	int[] viTinderboxesCodesArray;
	int[] viOilPotionsCodesArray;
	int[] viHealthPotionsCodesArray;
	int[] viSanityPotionsCodesArray;
	int[] viLargeOilPotionsCodesArray;
	int iChecker = (GetGlobalVarInt("FunnyModeOn") * 1) + (GetGlobalVarInt("EasyModeOn") * 2) + (GetGlobalVarInt("ToughModeOn") * 6);
	bool bFunnyModeOn = (iChecker == 1 || iChecker == 3 || iChecker == 5 || iChecker == 7 || iChecker == 13);
	bool bEasierModeOn = (iChecker == 4 || iChecker == 5);
	bool bEasyModeOn = (iChecker == 2 || iChecker == 3);
	bool bToughModeOn = (iChecker == 6 || iChecker == 7);
	bool bTougherModeOn = (iChecker == 12 || iChecker == 13);
	RemoveTimer("oilspeedquick");
	RemoveTimer("oilspeedslow");
	RemoveTimer("BlessingRandomGoodies");
	RemoveTimer("BlessingRegainHealth");
	RemoveTimer("BlessingRegainLanternOil");
	RemoveTimer("BlessingRegainSanity");
	RemoveTimer("CurseRandomGoodies");
	RemoveTimer("CurseRegainHealth");
	RemoveTimer("CurseRegainLanternOil");
	RemoveTimer("CurseRegainSanity");
	
	if(bEasierModeOn)
	{
		sPrefix = "Blessing";
		fDifficultyDirectFactor = 0.25f;
		fDifficultyInverseFactor = 1.75f;
		viTinderboxesCodesArray = gviEasierTinderboxesCodesArray;
		viOilPotionsCodesArray = gviEasierOilPotionsCodesArray;
		viHealthPotionsCodesArray = gviEasierHealthPotionsCodesArray;
		viSanityPotionsCodesArray = gviEasierSanityPotionsCodesArray;
		viLargeOilPotionsCodesArray = gviEasierLargeOilPotionsCodesArray;
		SetLanternLitCallback("LanternLitOilSpeedSlow");
		LanternLitOilSpeedSlow(GetLanternActive());
		SetEntityActive("oil_barrel_*", true);
		SetPlayerFallDamageDisabled(true);
		SetSanityDrainDisabled(true);
		AddTimer(sPrefix + "RandomGoodies", 60.0f, "TimerTunePlayerState");
		AddTimer(sPrefix + "RegainHealth", 2.0f, "TimerTunePlayerState");
		AddTimer(sPrefix + "RegainSanity", 3.0f, "TimerTunePlayerState");
		AddTimer(sPrefix + "RegainLanternOil", 4.0f, "TimerTunePlayerState");
	}
	else
	if(bEasyModeOn)
	{
		sPrefix = "Blessing";
		fDifficultyDirectFactor = 0.5f;
		fDifficultyInverseFactor = 1.5f;
		viTinderboxesCodesArray = gviEasyTinderboxesCodesArray;
		viOilPotionsCodesArray = gviEasyOilPotionsCodesArray;
		viHealthPotionsCodesArray = gviEasyHealthPotionsCodesArray;
		viSanityPotionsCodesArray = gviEasySanityPotionsCodesArray;
		viLargeOilPotionsCodesArray = gviEasyLargeOilPotionsCodesArray;
		SetLanternLitCallback("LanternLitOilSpeedSlow");
		LanternLitOilSpeedSlow(GetLanternActive());
		SetEntityActive("oil_barrel_*", true);
		SetPlayerFallDamageDisabled(true);
		SetSanityDrainDisabled(false);
		//AddTimer(sPrefix + "RandomGoodies", 60.0f, "TimerTunePlayerState");
		AddTimer(sPrefix + "RegainHealth", 2.0f, "TimerTunePlayerState");
		AddTimer(sPrefix + "RegainSanity", 3.0f, "TimerTunePlayerState");
		//AddTimer(sPrefix + "RegainLanternOil", 4.0f, "TimerTunePlayerState");
	}
	else
	if(bToughModeOn)
	{
		sPrefix = "Curse";
		fDifficultyDirectFactor = 1.5f;
		fDifficultyInverseFactor = 0.5f;
		viTinderboxesCodesArray = gviToughTinderboxesCodesArray;
		viOilPotionsCodesArray = gviToughOilPotionsCodesArray;
		viHealthPotionsCodesArray = gviToughHealthPotionsCodesArray;
		viSanityPotionsCodesArray = gviToughSanityPotionsCodesArray;
		viLargeOilPotionsCodesArray = gviToughLargeOilPotionsCodesArray;
		SetLanternLitCallback("LanternLitOilSpeedQuick");
		LanternLitOilSpeedQuick(GetLanternActive());
		SetEntityActive("oil_barrel_*", true);
		SetPlayerFallDamageDisabled(false);
		SetSanityDrainDisabled(false);
		//AddTimer(sPrefix + "RandomGoodies", 60.0f, "TimerTunePlayerState");
		AddTimer(sPrefix + "RegainHealth", 2.0f, "TimerTunePlayerState");
		AddTimer(sPrefix + "RegainSanity", 3.0f, "TimerTunePlayerState");
		//AddTimer(sPrefix + "RegainLanternOil", 4.0f, "TimerTunePlayerState");
	}
	else
	if(bTougherModeOn)
	{
		sPrefix = "Curse";
		fDifficultyDirectFactor = 1.75f;
		fDifficultyInverseFactor = 0.25f;
		viTinderboxesCodesArray = gviTougherTinderboxesCodesArray;
		viOilPotionsCodesArray = gviTougherOilPotionsCodesArray;
		viHealthPotionsCodesArray = gviTougherHealthPotionsCodesArray;
		viSanityPotionsCodesArray = gviTougherSanityPotionsCodesArray;
		viLargeOilPotionsCodesArray = gviTougherLargeOilPotionsCodesArray;
		SetLanternLitCallback("LanternLitOilSpeedQuick");
		LanternLitOilSpeedQuick(GetLanternActive());
		SetEntityActive("oil_barrel_*", false);
		SetPlayerFallDamageDisabled(false);
		SetSanityDrainDisabled(false);
		AddTimer(sPrefix + "RandomGoodies", 60.0f, "TimerTunePlayerState");
		AddTimer(sPrefix + "RegainHealth", 2.0f, "TimerTunePlayerState");
		AddTimer(sPrefix + "RegainSanity", 3.0f, "TimerTunePlayerState");
		AddTimer(sPrefix + "RegainLanternOil", 4.0f, "TimerTunePlayerState");
	}
	else
	{
		sPrefix = "";
		fDifficultyDirectFactor = 1.0f;
		fDifficultyInverseFactor = 1.0f;
		viTinderboxesCodesArray = gviNormalTinderboxesCodesArray;
		viOilPotionsCodesArray = gviNormalOilPotionsCodesArray;
		viHealthPotionsCodesArray = gviNormalHealthPotionsCodesArray;
		viSanityPotionsCodesArray = gviNormalSanityPotionsCodesArray;
		viLargeOilPotionsCodesArray = gviNormalLargeOilPotionsCodesArray;
		SetLanternLitCallback("");
		//LanternLitOilSpeedNormal(GetLanternActive());
		SetEntityActive("oil_barrel_*", true);
		SetPlayerFallDamageDisabled(false);
		SetSanityDrainDisabled(false);
		//AddTimer(sPrefix + "RandomGoodies", 60.0f, "TimerTunePlayerState");
		//AddTimer(sPrefix + "RegainHealth", 2.0f, "TimerTunePlayerState");
		//AddTimer(sPrefix + "RegainSanity", 3.0f, "TimerTunePlayerState");
		//AddTimer(sPrefix + "RegainLanternOil", 4.0f, "TimerTunePlayerState");
	}
	
	SetDifficultyDirectFactor(fDifficultyDirectFactor);
	SetDifficultyInverseFactor(fDifficultyInverseFactor);
	//SetEnemyMinIdleTime(2.0f * fDifficultyDirectFactor);
	//DISABLES ALL GOODIES.
	if(gviEasierTinderboxesCodesArray[0] != 0)
	{
		for(int i=0; i<gviEasierTinderboxesCodesArray.length(); i++)
		{
			if(GetEntityExists(gsTinderbox + "_" + gviEasierTinderboxesCodesArray[i]))
			{
				SetEntityActive(gsTinderbox + "_" + gviEasierTinderboxesCodesArray[i], false);
			}
		}
	}
	
	if(gviEasierOilPotionsCodesArray[0] != 0)
	{
		for(int i=0; i<gviEasierOilPotionsCodesArray.length(); i++)
		{
			if(GetEntityExists(gsOilPotion + "_" + gviEasierOilPotionsCodesArray[i]))
			{
				SetEntityActive(gsOilPotion + "_" + gviEasierOilPotionsCodesArray[i], false);
			}
		}
	}
	
	if(gviEasierHealthPotionsCodesArray[0] != 0)
	{
		for(int i=0; i<gviEasierHealthPotionsCodesArray.length(); i++)
		{
			if(GetEntityExists(gsHealthPotion + "_" + gviEasierHealthPotionsCodesArray[i]))
			{
				SetEntityActive(gsHealthPotion + "_" + gviEasierHealthPotionsCodesArray[i], false);
			}
		}
	}
	
	if(gviEasierSanityPotionsCodesArray[0] != 0)
	{
		for(int i=0; i<gviEasierSanityPotionsCodesArray.length(); i++)
		{
			if(GetEntityExists(gsSanityPotion + "_" + gviEasierSanityPotionsCodesArray[i]))
			{
				SetEntityActive(gsSanityPotion + "_" + gviEasierSanityPotionsCodesArray[i], false);
			}
		}
	}
	
	if(gviEasierLargeOilPotionsCodesArray[0] != 0)
	{
		for(int i=0; i<gviEasierLargeOilPotionsCodesArray.length(); i++)
		{
			if(GetEntityExists(gsLargeOilPotion + "_" + gviEasierLargeOilPotionsCodesArray[i]))
			{
				SetEntityActive(gsLargeOilPotion + "_" + gviEasierLargeOilPotionsCodesArray[i], false);
			}
		}
	}
	//ENABLES SPECIFIC GOODIES BASED ON EXTRAMODES.
	if(viTinderboxesCodesArray[0] != 0)
	{
		for(int i=0; i<viTinderboxesCodesArray.length(); i++)
		{
			if(GetEntityExists(gsTinderbox + "_" + viTinderboxesCodesArray[i]))
			{
				SetEntityActive(gsTinderbox + "_" + viTinderboxesCodesArray[i], true);
			}
		}
	}
	
	if(viOilPotionsCodesArray[0] != 0)
	{
		for(int i=0; i<viOilPotionsCodesArray.length(); i++)
		{
			if(GetEntityExists(gsOilPotion + "_" + viOilPotionsCodesArray[i]))
			{
				SetEntityActive(gsOilPotion + "_" + viOilPotionsCodesArray[i], true);
			}
		}
	}
	
	if(viHealthPotionsCodesArray[0] != 0)
	{
		for(int i=0; i<viHealthPotionsCodesArray.length(); i++)
		{
			if(GetEntityExists(gsHealthPotion + "_" + viHealthPotionsCodesArray[i]))
			{
				SetEntityActive(gsHealthPotion + "_" + viHealthPotionsCodesArray[i], true);
			}
		}
	}
	
	if(viSanityPotionsCodesArray[0] != 0)
	{
		for(int i=0; i<viSanityPotionsCodesArray.length(); i++)
		{
			if(GetEntityExists(gsSanityPotion + "_" + viSanityPotionsCodesArray[i]))
			{
				SetEntityActive(gsSanityPotion + "_" + viSanityPotionsCodesArray[i], true);
			}
		}
	}
	
	if(viLargeOilPotionsCodesArray[0] != 0)
	{
		for(int i=0; i<viLargeOilPotionsCodesArray.length(); i++)
		{
			if(GetEntityExists(gsLargeOilPotion + "_" + viLargeOilPotionsCodesArray[i]))
			{
				SetEntityActive(gsLargeOilPotion + "_" + viLargeOilPotionsCodesArray[i], true);
			}
		}
	}
	
	if(gsCurrentMapName == "00_rainy_hall")
	{
		AddEntityCollideCallback("Player", "AreaExtraModes", "OnAreaExtraModesCollide", true, 1);
		SetLightVisible("ButtonBoxLight" + gsStandardSuffix, false);
		SetLightVisible("ButtonBoxLight" + gsStandardToughSuffix, false);
		SetLightVisible("ButtonBoxLight" + gsStandardTougherSuffix, false);
		SetLightVisible("ButtonBoxLight" + gsStandardEasySuffix, false);
		SetLightVisible("ButtonBoxLight" + gsStandardEasierSuffix, false);
		SetLightVisible("ButtonBoxLight" + gsFunnySuffix, false);
		SetLightVisible("ButtonBoxLight" + gsFunnyToughSuffix, false);
		SetLightVisible("ButtonBoxLight" + gsFunnyTougherSuffix, false);
		SetLightVisible("ButtonBoxLight" + gsFunnyEasySuffix, false);
		SetLightVisible("ButtonBoxLight" + gsFunnyEasierSuffix, false);
	}
	else
	if(gsCurrentMapName == "01_old_archives")
	{
		if(bEasierModeOn)
		{
			SetEntityActive("AreaHintLean", true);
		}
		else
		if(bEasyModeOn)
		{
			SetEntityActive("AreaHintLean", true);
		}
		else
		if(bToughModeOn)
		{
			SetEntityActive("AreaHintLean", true);
		}
		else
		if(bTougherModeOn)
		{
			SetEntityActive("AreaHintLean", true);
		}
		else
		{
			SetEntityActive("AreaHintLean", false);
		}
	}
	else
	if(gsCurrentMapName == "04_wine_cellar_lab")
	{
		if(bEasierModeOn)
		{
			SetFogActive(true);
			SetFogColor(0.16f, 0.18f, 0.22f, 0.7f);
			SetFogProperties(6.0f, 24.0f, 1.0f, false);
		}
		else
		if(bEasyModeOn)
		{
			SetFogActive(true);
			SetFogColor(0.14f, 0.16f, 0.2f, 0.7f);
			SetFogProperties(5.0f, 20.0f, 1.0f, false);
		}
		else
		if(bToughModeOn)
		{
			SetFogActive(true);
			SetFogColor(0.1f, 0.12f, 0.16f, 0.7f);
			SetFogProperties(3.0f, 12.0f, 1.0f, false);
		}
		else
		if(bTougherModeOn)
		{
			SetFogActive(true);
			SetFogColor(0.08f, 0.1f, 0.14f, 0.7f);
			SetFogProperties(2.0f, 8.0f, 1.0f, false);
		}
		else
		{
			SetFogActive(true);
			SetFogColor(0.12f, 0.14f, 0.18f, 0.7f);
			SetFogProperties(4.0f, 16.0f, 1.0f, false);
		}
	}
	else
	if(gsCurrentMapName == "05_wine_cellar")
	{
		if(bEasierModeOn)
		{
			SetEntityActive("chest_small_2", true);
			SetFogActive(true);
			SetFogColor(0.15f, 0.16f, 0.18f, 1.0f);
			SetFogProperties(10.0f, 30.0f, 1.0f, false);
		}
		else
		if(bEasyModeOn)
		{
			SetEntityActive("chest_small_2", true);
			SetFogActive(true);
			SetFogColor(0.15f, 0.16f, 0.18f, 1.0f);
			SetFogProperties(8.0f, 26.0f, 1.0f, false);
		}
		else
		if(bToughModeOn)
		{
			SetEntityActive("chest_small_2", false);
			SetFogActive(true);
			SetFogColor(0.15f, 0.16f, 0.18f, 1.0f);
			SetFogProperties(4.0f, 18.0f, 1.0f, false);
		}
		else
		if(bTougherModeOn)
		{
			SetEntityActive("chest_small_2", false);
			SetFogActive(true);
			SetFogColor(0.15f, 0.16f, 0.18f, 1.0f);
			SetFogProperties(2.0f, 14.0f, 1.0f, false);
		}
		else
		{
			SetEntityActive("chest_small_2", false);
			SetFogActive(true);
			SetFogColor(0.15f, 0.16f, 0.18f, 1.0f);
			SetFogProperties(6.0f, 22.0f, 1.0f, false);
		}
	}
	else
	if(gsCurrentMapName == "06_distillery")
	{
		if(bEasierModeOn)
		{
			SetFogActive(true);
			SetFogColor(0.16f, 0.18f, 0.22f, 0.7f);
			SetFogProperties(6.0f, 24.0f, 1.0f, true);
		}
		else
		if(bEasyModeOn)
		{
			SetFogActive(true);
			SetFogColor(0.14f, 0.16f, 0.2f, 0.7f);
			SetFogProperties(5.0f, 20.0f, 1.0f, true);
		}
		else
		if(bToughModeOn)
		{
			SetFogActive(true);
			SetFogColor(0.1f, 0.12f, 0.16f, 0.7f);
			SetFogProperties(3.0f, 12.0f, 1.0f, true);
		}
		else
		if(bTougherModeOn)
		{
			SetFogActive(true);
			SetFogColor(0.08f, 0.1f, 0.14f, 0.7f);
			SetFogProperties(2.0f, 8.0f, 1.0f, true);
		}
		else
		{
			SetFogActive(true);
			SetFogColor(0.12f, 0.14f, 0.18f, 0.7f);
			SetFogProperties(4.0f, 16.0f, 1.0f, true);
		}
	}
	else
	if(gsCurrentMapName == "07_archives_cellar")
	{
		if(bEasierModeOn)
		{
			SetLocalVarInt("MeatReducer", 0);
		}
		else
		if(bEasyModeOn)
		{
			SetLocalVarInt("MeatReducer", 0);
		}
		else
		if(bToughModeOn)
		{
			SetLocalVarInt("MeatReducer", 2);
		}
		else
		if(bTougherModeOn)
		{
			SetLocalVarInt("MeatReducer", 3);
		}
		else
		{
			SetLocalVarInt("MeatReducer", 1);
		}
	}
	else
	if(gsCurrentMapName == "12_storage")
	{
		if(bEasierModeOn)
		{
			SetFogActive(false);
			SetSkyBoxActive(false);
			SetLocalVarInt("GruntDoorMaxBangs", 3);
		}
		else
		if(bEasyModeOn)
		{
			SetFogActive(true);
			SetFogProperties(4.0f, 35.0f, 1.2f, false);
			SetSkyBoxActive(true);
			SetLocalVarInt("GruntDoorMaxBangs", 3);
		}
		else
		if(bToughModeOn)
		{
			SetFogActive(true);
			SetFogProperties(2.0f, 29.0f, 0.8f, false);
			SetSkyBoxActive(true);
			SetLocalVarInt("GruntDoorMaxBangs", 1);
		}
		else
		if(bTougherModeOn)
		{
			SetFogActive(true);
			SetFogProperties(1.0f, 26.0f, 0.6f, false);
			SetSkyBoxActive(true);
			SetLocalVarInt("GruntDoorMaxBangs", 1);
		}
		else
		{
			SetFogActive(true);
			SetFogProperties(3.0f, 32.0f, 1.0f, false);
			SetSkyBoxActive(true);
			SetLocalVarInt("GruntDoorMaxBangs", 2);
		}
	}
	else
	if(gsCurrentMapName == "15_prison_north")
	{
		if(bEasierModeOn)
		{
			if(GetGlobalVarInt("PrisonTinderboxCollected") != 1)
			{
				SetEntityActive("tinderbox_13", true);
			}	
			else
			{
				SetEntityActive("tinderbox_13", false);
			}
		}
		else
		if(bEasyModeOn)
		{
			if(GetGlobalVarInt("PrisonTinderboxCollected") != 1)
			{
				SetEntityActive("tinderbox_13", true);
			}	
			else
			{
				SetEntityActive("tinderbox_13", false);
			}
		}
		else
		if(bToughModeOn)
		{
			SetEntityActive("tinderbox_13", false);
		}
		else
		if(bTougherModeOn)
		{
			SetEntityActive("tinderbox_13", false);
		}
		else
		{
			if(GetGlobalVarInt("PrisonTinderboxCollected") != 1)
			{
				SetEntityActive("tinderbox_13", true);
			}	
			else
			{
				SetEntityActive("tinderbox_13", false);
			}
		}
	}
	else
	if(gsCurrentMapName == "20_sewer")
	{
		if(bEasierModeOn)
		{
			SetFogProperties(9.0f, 28.0f, 1.2f, true);
		}
		else
		if(bEasyModeOn)
		{
			SetFogProperties(7.0f, 24.0f, 1.1f, true);
		}
		else
		if(bToughModeOn)
		{
			SetFogProperties(3.0f, 16.0f, 0.9f, true);
		}
		else
		if(bTougherModeOn)
		{
			SetFogProperties(1.0f, 12.0f, 0.8f, true);
		}
		else
		{
			SetFogProperties(5.0f, 20.0f, 1.0f, true);
		}
	}
	else
	if(gsCurrentMapName == "23_torture_transept")
	{
		if(bEasierModeOn)
		{
			SetFogProperties(5.0f, 20.0f, 1.2f, true);
		}
		else
		if(bEasyModeOn)
		{
			SetFogProperties(4.0f, 17.5f, 1.1f, true);
		}
		else
		if(bToughModeOn)
		{
			SetFogProperties(2.0f, 12.5f, 0.9f, true);
		}
		else
		if(bTougherModeOn)
		{
			SetFogProperties(1.0f, 10.0f, 0.8f, true);
		}
		else
		{
			SetFogProperties(3.0f, 15.0f, 1.0f, true);
		}
	}
	else
	if(gsCurrentMapName == "24_torture_choir_east")
	{
		if(bEasierModeOn)
		{
			SetFogProperties(6.0f, 45.0f, 0.4f, true);
			SetLocalVarFloat("MinEnemyPlayerDistance", 47.0f);
		}
		else
		if(bEasyModeOn)
		{
			SetFogProperties(4.5f, 37.5f, 0.35f, true);
			SetLocalVarFloat("MinEnemyPlayerDistance", 39.0f);
		}
		else
		if(bToughModeOn)
		{
			SetFogProperties(1.5f, 22.5f, 0.25f, true);
			SetLocalVarFloat("MinEnemyPlayerDistance", 23.0f);
		}
		else
		if(bTougherModeOn)
		{
			SetFogProperties(0.0f, 15.0f, 0.2f, true);
			SetLocalVarFloat("MinEnemyPlayerDistance", 15.0f);
		}
		else
		{
			SetFogProperties(3.0f, 30.0f, 0.3f, true);
			SetLocalVarFloat("MinEnemyPlayerDistance", 31.0f);
		}
	}
}

void SetExtraModesOn(string asInput) //HELPER FUNCTION TO ENABLE THE CORRECT EXTRAMODES, ITS BASED ON THE EXTRAMODES SUFFIX CONTAINED INSIDE THE ARGUMENT.
{
	string sSuffix = "ModeChosen";
	
	if(StringContains(asInput, gsFunnyEasierSuffix))
	{
		SetFunnyModeOn(true);
		SetEasyModeOn(2);
		SetToughModeOn(0);
		SetMessage("MainMenu", gsFunnyEasierEntry + sSuffix, 8.0f);
	}
	else
	if(StringContains(asInput, gsFunnyEasySuffix))
	{
		SetFunnyModeOn(true);
		SetEasyModeOn(1);
		SetToughModeOn(0);
		SetMessage("MainMenu", gsFunnyEasyEntry + sSuffix, 8.0f);
	}
	else
	if(StringContains(asInput, gsFunnyTougherSuffix))
	{
		SetFunnyModeOn(true);
		SetEasyModeOn(0);
		SetToughModeOn(2);
		SetMessage("MainMenu", gsFunnyTougherEntry + sSuffix, 8.0f);
	}
	else
	if(StringContains(asInput, gsFunnyToughSuffix))
	{
		SetFunnyModeOn(true);
		SetEasyModeOn(0);
		SetToughModeOn(1);
		SetMessage("MainMenu", gsFunnyToughEntry + sSuffix, 8.0f);
	}
	else
	if(StringContains(asInput, gsFunnySuffix))
	{
		SetFunnyModeOn(true);
		SetEasyModeOn(0);
		SetToughModeOn(0);
		SetMessage("MainMenu", gsFunnyEntry + sSuffix, 8.0f);
	}
	else
	if(StringContains(asInput, gsStandardEasierSuffix))
	{
		SetFunnyModeOn(false);
		SetEasyModeOn(2);
		SetToughModeOn(0);
		SetMessage("MainMenu", gsStandardEasierEntry + sSuffix, 8.0f);
	}
	else
	if(StringContains(asInput, gsStandardEasySuffix))
	{
		SetFunnyModeOn(false);
		SetEasyModeOn(1);
		SetToughModeOn(0);
		SetMessage("MainMenu", gsStandardEasyEntry + sSuffix, 8.0f);
	}
	else
	if(StringContains(asInput, gsStandardTougherSuffix))
	{
		SetFunnyModeOn(false);
		SetEasyModeOn(0);
		SetToughModeOn(2);
		SetMessage("MainMenu", gsStandardTougherEntry + sSuffix, 8.0f);
	}
	else
	if(StringContains(asInput, gsStandardToughSuffix))
	{
		SetFunnyModeOn(false);
		SetEasyModeOn(0);
		SetToughModeOn(1);
		SetMessage("MainMenu", gsStandardToughEntry + sSuffix, 8.0f);
	}
	else
	if(StringContains(asInput, gsStandardSuffix))
	{
		SetFunnyModeOn(false);
		SetEasyModeOn(0);
		SetToughModeOn(0);
		SetMessage("MainMenu", gsStandardEntry + sSuffix, 8.0f);
	}
	
	SetupCurrentMapBasedOnExtraModes();
}

////////////////
////////////////

float GetEnemyIdleTime(float afTime)
{
	if(GetEasyModeOn() >= 2)
	{
		return afTime;
	}
	else
	if(GetToughModeOn() >= 2)
	{
		return (RandFloat(((GetDifficultyDirectFactor() * afTime) * 1.0f), ((GetDifficultyDirectFactor() * afTime) * 2.0f)));
	}
	else
	{
		return (GetDifficultyDirectFactor() * afTime);
	}
}

int GetPlayerGlobalDeathCount() //RETURNS THE VALUE OF THE PLAYER GLOBAL DEATH COUNT.
{
	return GetGlobalVarInt("PlayerGlobalDeathCount");
}

void SetPlayerGlobalDeathCount(int aiCount) //CHANGES THE VALUE OF THE PLAYER GLOBAL DEATH COUNT.
{
	SetGlobalVarInt("PlayerGlobalDeathCount", aiCount);
}

bool GetHardModeEndingOn()
{
	return ((GetToughModeOn() == 1 && (GetPlayerGlobalDeathCount() + 1) > giMaxGlobalDeathCount) || (GetToughModeOn() == 2));
}

void StartHardModeEnding(string asInput)
{
	if(asInput == "HardModeDeath")
	{
		StartCredits("", false, "Hints", "HardModeDeath", 3);
	}
	else
	{
		ChangePlayerStateToNormal();
		ExitInventory();
		FadeGlobalSoundVolume(0.0f, 0.01f);
		FadeOut(0.01f);
		SetInDarknessEffectsActive(false);
		SetInventoryDisabled(true);
		SetLanternActive(false, false);
		SetLanternDisabled(true);
		SetPlayerActive(false);
		SetPlayerHealth(999.999f);
		SetPlayerLampOil(999.999f);
		SetPlayerSanity(999.999f);
		SetSanityDrainDisabled(true);
		ShowPlayerCrossHairIcons(false);
		StopMusic(0.01f, 0);
		StopMusic(0.01f, 1);
		StopMusic(0.01f, 2);
		StopMusic(0.01f, 3);
		StopMusic(0.01f, 4);
		StopMusic(0.01f, 5);
		StopMusic(0.01f, 6);
		StopMusic(0.01f, 7);
		StopMusic(0.01f, 8);
		StopMusic(0.01f, 9);
		StopMusic(0.01f, 10);
		StopPlayerLookAt();
		AddTimer("HardModeDeath", 6.0f, "StartHardModeEnding");
	}
}

void OnPlayerDeath(string asName) //COUNTS HOW MANY TIMES THE PLAYER DIED THROUGH THE WHOLE STORY. CONTAINS THE HARD MODE GAME OVER ENDING LOGIC.
{
	if(GetHardModeEndingOn())
	{
		StartHardModeEnding("");
		return;
	}
	
	SetPlayerGlobalDeathCount(GetPlayerGlobalDeathCount() + 1);
}

//CURRENT MAP SPECIFIC
void TimerExtraModesDiscovered(string asTimer)
{
	if(asTimer == "FadeIn")
	{
		FadeIn(4.0f);
	}
	if(asTimer == "StopPlayerLookAt")
	{
		StopPlayerLookAt();
		SetPlayerMoveSpeedMul(1.0f);
	}
}

int iTriggered = 0;

void OnAreaExtraModesCollide(string asParent, string asChild, int aiState)
{
	if(iTriggered >= 1)
	{
		return;
	}
	
	iTriggered = iTriggered + 1;
	
	if(RandInt(0, 1) == 0)
	{
		StartPlayerLookAt("button" + gsStandardTougherSuffix, 1.5f, 2.0f, "");
	}
	else
	{
		StartPlayerLookAt("button" + gsFunnyEasierSuffix, 1.5f, 2.0f, "");
	}
	
	float fSanity = GetPlayerSanity();
	SetPlayerSanity(fSanity - 10.0f);
	AddPlayerSanity(10.0f);
	SetPlayerMoveSpeedMul(0.05f);
	FadeOut(0.0f);
	PlayGuiSound("quest_completed.snt", 0.5f);
	SetLightVisible("ButtonBoxLight" + gsStandardSuffix, true);
	SetLightVisible("ButtonBoxLight" + gsStandardToughSuffix, true);
	SetLightVisible("ButtonBoxLight" + gsStandardTougherSuffix, true);
	SetLightVisible("ButtonBoxLight" + gsStandardEasySuffix, true);
	SetLightVisible("ButtonBoxLight" + gsStandardEasierSuffix, true);
	SetLightVisible("ButtonBoxLight" + gsFunnySuffix, true);
	SetLightVisible("ButtonBoxLight" + gsFunnyToughSuffix, true);
	SetLightVisible("ButtonBoxLight" + gsFunnyTougherSuffix, true);
	SetLightVisible("ButtonBoxLight" + gsFunnyEasySuffix, true);
	SetLightVisible("ButtonBoxLight" + gsFunnyEasierSuffix, true);
	SetLampLit("candlestick_floor_18", true, true);
	SetLampLit("candlestick_floor_19", true, true);
	AddTimer("FadeIn", 0.1f, "TimerExtraModesDiscovered");
	AddTimer("StopPlayerLookAt", 2.0f, "TimerExtraModesDiscovered");
}

void TimerResetButton(string asTimer)
{
	if(StringContains(asTimer, "ButtonBoxLight"))
	{
		if(StringContains(asTimer, gsFunnySuffix))
		{
			FadeLightTo(asTimer, 0.0f, 0.5f, 1.0f, 1.0f, -1, 0.75f);
		}
		else
		{
			FadeLightTo(asTimer, 1.0f, 0.5f, 0.0f, 1.0f, -1, 0.75f);
		}
	}
	else
	{
		SetEntityInteractionDisabled("button" + gsStandardSuffix, false);
		SetEntityInteractionDisabled("button" + gsStandardToughSuffix, false);
		SetEntityInteractionDisabled("button" + gsStandardTougherSuffix, false);
		SetEntityInteractionDisabled("button" + gsStandardEasySuffix, false);
		SetEntityInteractionDisabled("button" + gsStandardEasierSuffix, false);
		SetEntityInteractionDisabled("button" + gsFunnySuffix, false);
		SetEntityInteractionDisabled("button" + gsFunnyToughSuffix, false);
		SetEntityInteractionDisabled("button" + gsFunnyTougherSuffix, false);
		SetEntityInteractionDisabled("button" + gsFunnyEasySuffix, false);
		SetEntityInteractionDisabled("button" + gsFunnyEasierSuffix, false);
	}
}

void OnButtonPlayerInteract(string asEntity)
{
	SetExtraModesOn(asEntity);
	SetEntityInteractionDisabled("button" + gsStandardSuffix, true);
	SetEntityInteractionDisabled("button" + gsStandardToughSuffix, true);
	SetEntityInteractionDisabled("button" + gsStandardTougherSuffix, true);
	SetEntityInteractionDisabled("button" + gsStandardEasySuffix, true);
	SetEntityInteractionDisabled("button" + gsStandardEasierSuffix, true);
	SetEntityInteractionDisabled("button" + gsFunnySuffix, true);
	SetEntityInteractionDisabled("button" + gsFunnyToughSuffix, true);
	SetEntityInteractionDisabled("button" + gsFunnyTougherSuffix, true);
	SetEntityInteractionDisabled("button" + gsFunnyEasySuffix, true);
	SetEntityInteractionDisabled("button" + gsFunnyEasierSuffix, true);
	PlayGuiSound("lock_door.snt", 1.0f);
	string sLight = GetStringWithExtraModesSuffix("ButtonBoxLight");
	FadeLightTo(sLight, 0.0f, 1.0f, 0.0f, 1.0f, -1, 0.25f);
	AddTimer(sLight, 0.25f, "TimerResetButton");
	AddTimer(asEntity, 1.0f, "TimerResetButton");
}

//-------------------------------------------------

//////////////////////////////
// CUSTOM CODE END
//////////////////////////////

//-------------------------------------------------

//---------------------------------------------

////////////////////////
// GENERAL
////////////////////////

//---------------------------------------------

void PlayEffectVoice(string &in asEntryBase,int alStartIdx, int alEndIdx, string &in asCallback)
{	
	for(int i=alStartIdx; i<=alEndIdx; ++i)
	{
		string sEntry = asEntryBase;
		if(i<10) sEntry += "0";
		sEntry += i;
		
		AddEffectVoice(sEntry, "", "Flashbacks", sEntry, false, "", 0,0 );
	}

	SetEffectVoiceOverCallback(asCallback);
}

//---------------------------------------------

////////////////////////
// INTRO FLASHBACKS
////////////////////////

//---------------------------------------------

float gfIntroSequenceGlobalVolume = 0.3f;
float gfIntroSequenceFadeOutTime = 3;

//---------------------------------------------

void TimerStartGame(string &in asTimer)
{
	PlayMusic("23_amb.ogg", true, 0.7f, 1, 0, false);
			
	AddTimer("IntroSequence", 2, "TimerIntroSequence");
}

void ResumeIntroSequence()
{
	AddTimer("IntroSequence", 0.1f, "TimerIntroSequence");
}

//---------------------------------------------

void TimerIntroSequence(string &in asTimer)
{
	int lState = GetLocalVarInt("IntroSequenceState");
	AddLocalVarInt("IntroSequenceState",1);
	float fNextEventTime = 1.0f;
	bool bPause = false;
	
	////////////////////////
	// 0: Fade In #1
	if(lState ==0)
	{
		FadeIn(3);
		SetPlayerActive(true);
		
		SetPlayerMoveSpeedMul(0.35f);
		SetPlayerRunSpeedMul(0);
		//SetPlayerLookSpeedMul(0.5);
		
		FadeImageTrailTo(2,1);
		FadeSepiaColorTo(1,1);	
		
		FadePlayerRollTo(12, 0.3f,0.7f); 
		FadePlayerFOVMulTo(0.7, 0.05);
		
		FadeGlobalSoundVolume(gfIntroSequenceGlobalVolume, 3);
		
		PlayGuiSound("fb_sfx_00_daniel.ogg", 1.0f);
		PlayEffectVoice("CH01L00_DanielsMind01_", 1, 1, "IntroSequenceVoiceOver");
		PlayEffectVoice("CH01L00_DanielsMind02_", 1, 1, "IntroSequenceVoiceOver");
		PlayEffectVoice("CH01L00_DanielsMind03_", 1, 1, "IntroSequenceVoiceOver");
		PlayEffectVoice("CH01L00_DanielsMind04_", 1, 1, "IntroSequenceVoiceOver");
		
		fNextEventTime = 5.0f;
		//bPause = true;		
	}
	////////////////////////
	// 1: Fade Out #1
	else if(lState ==1)
	{
		FadeOut(gfIntroSequenceFadeOutTime);
		FadeGlobalSoundVolume(0, gfIntroSequenceFadeOutTime);
		
		fNextEventTime = gfIntroSequenceFadeOutTime + 0.1;
	}
	////////////////////////
	// 2: Fade In #2
	else if(lState ==2)
	{
		FadeIn(3);
		TeleportPlayer("IntroStart_2");
		
		FadePlayerRollTo(-12, 0.3f, 0.8f); 
		FadePlayerFOVMulTo(1.2, 0.03);
		
		FadeGlobalSoundVolume(gfIntroSequenceGlobalVolume, 3);
		
		fNextEventTime = 5.0f;
		//PlayEffectVoice("CH01L00_DanielsMind02_", 1, 1, "IntroSequenceVoiceOver");
		//bPause = true;		
	}
	////////////////////////
	// 3: Fade Out #2
	else if(lState ==3)
	{
		FadeOut(gfIntroSequenceFadeOutTime);
		FadeGlobalSoundVolume(0, gfIntroSequenceFadeOutTime);
		
		fNextEventTime = gfIntroSequenceFadeOutTime + 0.1;
	}
	////////////////////////
	// 4: Fade In #3
	else if(lState ==4)
	{
		FadeIn(3);
		TeleportPlayer("IntroStart_3");
		
		FadePlayerRollTo(12, 0.4f, 0.5f); 
		FadePlayerFOVMulTo(0.5, 0.05);
		
		FadeGlobalSoundVolume(gfIntroSequenceGlobalVolume, 3);
		
		fNextEventTime = 5.0f;
		//PlayEffectVoice("CH01L00_DanielsMind03_", 1, 1, "IntroSequenceVoiceOver");
		//bPause = true;		
	}
	////////////////////////
	// 5: Fade Out #3
	else if(lState ==5)
	{
		FadeOut(gfIntroSequenceFadeOutTime);
		FadeGlobalSoundVolume(0, gfIntroSequenceFadeOutTime);
		
		fNextEventTime = gfIntroSequenceFadeOutTime + 0.1;
	}
	////////////////////////
	// 6: Fade In #4
	else if(lState ==6)
	{
		FadeIn(3);
		TeleportPlayer("IntroStart_4");
		
		FadePlayerRollTo(-12, 0.4f, 0.8f); 
		FadePlayerFOVMulTo(1.3, 0.1);
		
		FadeGlobalSoundVolume(gfIntroSequenceGlobalVolume, 3);
		
		fNextEventTime = 6.0f;
		//PlayEffectVoice("CH01L00_DanielsMind04_", 1, 1, "IntroSequenceVoiceOver");
		//bPause = true;		
	}
	////////////////////////
	// 7: Fade Out #4
	else if(lState ==7)
	{
		FadeOut(gfIntroSequenceFadeOutTime);
		FadeGlobalSoundVolume(0, gfIntroSequenceFadeOutTime);
		
		AddTimer("WakeUpPlayer", gfIntroSequenceFadeOutTime+4, "TimerWakeUpPlayer");
		AddTimer("activefix", gfIntroSequenceFadeOutTime, "TimerActiveFix");
		bPause = true;
		return;
	}
	
	AddDebugMessage("Event:"+lState+" Time:"+fNextEventTime,false);
	
	if(bPause==false)
		AddTimer("IntroSequence", fNextEventTime, "TimerIntroSequence");
}

void TimerActiveFix(string &in asTimer)
{
	SetPlayerActive(false);
}

//---------------------------------------------

void IntroSequenceVoiceOver()
{
	ResumeIntroSequence();
}

//---------------------------------------------

void TimerWakeUpPlayer(string &in asTimer)
{
	TeleportPlayer("PlayerStartArea_1");
	
	StopMusic(3, 0);
	
	PlayGuiSound("player_bodyfall5.ogg",1);
	
	FadeImageTrailTo(0,1);
	FadeSepiaColorTo(0,1);
	
	FadePlayerRollTo(0, 1, 3); 
	FadePlayerFOVMulTo(1, 1);
		
	FadeGlobalSoundVolume(1, 3);
	FadeGlobalSoundSpeed(1, 3);
	
	SetInventoryDisabled(false);
	
	SetEntityActive("narrowpassout", true);
	SetEntityActive("gallerydoor", true);
	SetEntityActive("areanarrowhalls", true);
	SetEntityActive("areaendthunder", true);
	SetEntityActive("areabeginthunder", true);
	SetEntityActive("areagustdoor", true);
	SetEntityActive("AreaDust3", true);
	SetEntityActive("AreaFaint", true);
	
	SetPlayerJumpDisabled(true);
	SetPlayerCrouchDisabled(true);
	SetPlayerRunSpeedMul(0);
	FadePlayerRollTo(75, 10, 100); 	//positivt är roll från up mot vänster i grader. roll, speedX, maxspeed
	MovePlayerHeadPos(-0.2f, -1.3f, 0, 10, 0.5f);	//x(0 def), y(0 def), z(0 def), speed, slowdowndist
	StartPlayerLookAt("Arealook1", 10, 100, "");	//Area,speedX,maxspeed,callback()	
	
	AddTimer("blackout", 9.0f, "TimerBlackOut");
	AddTimer("forhelbeep", 5.2f, "TimerBeginSoundsIntro");
}

//---------------------------------------------

////////////////////////
// WAKE UP
////////////////////////

//---------------------------------------------

//NOTE: The following are not to be used! just letting the info be here so can use later.
/*void TimerSFXIntro(string &in asTimer)
{
	PlayGuiSound("fb_sfx_00_daniel.ogg", 1.0f);
}
void TimerVoiceIntro(string &in asTimer)
{
	AddEffectVoice("CH01L00_DanielsMind01_01.ogg", "", "Flashbacks", "CH01L00_DanielsMind01_01", false, "", 0, 0);
	AddEffectVoice("CH01L00_DanielsMind02_01.ogg", "", "Flashbacks", "CH01L00_DanielsMind02_01", false, "", 0, 0);
	AddEffectVoice("CH01L00_DanielsMind03_01.ogg", "", "Flashbacks", "CH01L00_DanielsMind03_01", false, "", 0, 0);
	AddEffectVoice("CH01L00_DanielsMind04_01.ogg", "", "Flashbacks", "CH01L00_DanielsMind04_01", false, "", 0, 0);

	SetEffectVoiceOverCallback("VoiceOverIntro");
}*/

void TimerBeginSoundsIntro(string &in asTimer)
{
	PlaySoundAtEntity("Rain", "general_rain_m.snt", "AreaThunder", 4, true);
	PlaySoundAtEntity("Eerie", "ambience_wind_eerie_no3d.snt", "AreaThunder", 4, true);
	PlaySoundAtEntity("Hollow", "ambience_hollow_tinker.snt", "AreaThunder", 4, true);
	PlaySoundAtEntity("Flow", "12_epoxy_flow.snt", "AreaFlowWater", 12, true);
}

//////////////////////////////////////////////
/*PLayer wakes up on floor and slowly gets up, when up receives first quest.
 */
void TimerBlackOut(string &in asTimer)
{
	AddLocalVarInt("BlackoutStep", 1);	//What step to play in the event
	float fEventSpeed = 0.5f;				//The default time between steps in an event

	switch(GetLocalVarInt("BlackoutStep")) {
		case 1:
			StartPlayerLookAt("Arealook2", 0.1f, 0.1f, "");
			FadeIn(4);
			FadeImageTrailTo(2,1);
			AddTimer("rose", 0.5f, "TimerRose");
			SetPlayerActive(true);
			ShowPlayerCrossHairIcons(true);
			SetPlayerMoveSpeedMul(0.05f);
			SetPlayerLookSpeedMul(0.05f);
			fEventSpeed = 3.0f;	
		break;
		case 2:
			FadePlayerRollTo(85, 1, 1); 
		break;
		case 3:
			StartPlayerLookAt("Arealook3", 0.1f, 0.1f, "");
		break;
		case 4:
			FadeImageTrailTo(0,1);
			FadePlayerRollTo(65, 1, 1); 
			AddTimer("thunder", 1, "TimerThunder");
		break;
		case 5:
			PlaySoundAtEntity("sigh", "react_sigh.snt", "Player", 1.0 / 2, false);
			FadeOut(2);
			fEventSpeed = 1.5f;	
		break;
		case 6:
			FadePlayerRollTo(85, 1, 4); 
			StartPlayerLookAt("Arealook1", 0.1f, 0.1f, "");
		break;
		case 7:
			FadeImageTrailTo(1.8f,1.5f);
			FadePlayerFOVMulTo(1.25f, 0.01);
		break;
		case 8:
			FadePlayerRollTo(45, 1, 2); 
			FadeIn(2);
			fEventSpeed = 1.5f;	
		break;
		case 9:
			StartPlayerLookAt("Arealook2", 0.1f, 0.1f, "");
		break;
		case 10:
			FadePlayerRollTo(15, 1, 2); 
			FadePlayerFOVMulTo(0.75f, 0.01);
		break;
		case 11:
			PlaySoundAtEntity("sigh", "react_sigh.snt", "Player", 1.0 / 1.5f, false);
			FadeOut(1);
			StartPlayerLookAt("Arealook3", 1, 1, "");
			FadePlayerRollTo(50, 1, 2); 
			fEventSpeed = 2.0f;	
		break;
		case 12:
			SetPlayerMoveSpeedMul(0.1f);
			SetPlayerLookSpeedMul(0.1f);
			StartPlayerLookAt("Arealook1", 1, 1, "");
			FadePlayerFOVMulTo(1.1f, 0.01);
			FadeImageTrailTo(0,1.5f);
			fEventSpeed = 1.5f;	
		break;
		case 13:
			SetPlayerMoveSpeedMul(0.2f);
			SetPlayerLookSpeedMul(0.2f);
			FadePlayerRollTo(-15, 1, 2); 
			FadeIn(1);
			StartPlayerLookAt("Arealook4", 2, 2, "");
			fEventSpeed = 2.0f;	
		break;
		case 14:
			SetPlayerMoveSpeedMul(0.3f);
			SetPlayerLookSpeedMul(0.4f);
			FadePlayerRollTo(-30, 10, 60); 	
			MovePlayerHeadPos(0, 0, 0, 1, 0.5f);	
			StartPlayerLookAt("Arealook3", 1, 1, "");
			FadePlayerFOVMulTo(0.9f, 0.01);
			FadeImageTrailTo(1.5,2);
			PlaySoundAtEntity("movement", "player_climb.snt", "Player", 0, false);
		break;
		case 15:
			SetPlayerMoveSpeedMul(0.4f);
			SetPlayerLookSpeedMul(0.6f);
			FadePlayerRollTo(10, 10, 20); 	
			MovePlayerHeadPos(0, -0.5f, 0, 1, 0.5f);
		break;
		case 16:
			SetPlayerMoveSpeedMul(0.5f);
			SetPlayerLookSpeedMul(0.8f);
			FadePlayerRollTo(0, 10, 60); 	
			MovePlayerHeadPos(0, 0, 0, 1, 0.5f);
			StartPlayerLookAt("Arealook4", 2, 2, "");
			FadePlayerFOVMulTo(1, 0.01f);
			PlaySoundAtEntity("movement", "player_climb.snt", "Player", 0, false);
			fEventSpeed = 2.0f;	
		break;
		case 17:
			SetPlayerJumpDisabled(false);
			SetPlayerCrouchDisabled(false);
			SetPlayerMoveSpeedMul(0.75f);
			SetPlayerLookSpeedMul(1.0f);
			FadeImageTrailTo(0,0.2f);
			StopPlayerLookAt();
			PlaySoundAtEntity("sigh", "react_sigh.snt", "Player", 1.0 / 1, false);
			AddTimer("lookloop", RandFloat(2.0f,6.0f), "TimerRandomLook");	//Activate the spinning head
			fEventSpeed = 1.0f;		
		break;
		case 18:
			SetEntityActive("AreaQuest", true);
			LookAtQuest("", 0);
		break;
	}
	
	if(GetLocalVarInt("BlackoutStep") < 19)  AddTimer("blackout", fEventSpeed, "TimerBlackOut");
}
void TimerRose(string &in asTimer)
{
	CreateParticleSystemAtEntity("rose", "ps_rose_petals.ps", "AreaRose", false);
}

//---------------------------------------------

////////////////////////////////////
//BEGIN THUNDER
////////////////////////////////////

//---------------------------------------------

/*Random lightning and thunder at start of level
 */
void TimerThunder(string &in asTimer)
{
	AddLocalVarInt("ThunderStep", 1);				//What step to play in the event
	float fEventSpeed = RandFloat(0.05f, 0.15f);	//The default time between steps in an event

	switch(GetLocalVarInt("ThunderStep")) {
		case 1:
			ThunderLights(2,0.05f);
		break;
		case 2:
			ThunderLights(1,0.05f);
		break;
		case 3:
			ThunderLights(3,0.05f);
		break;
		case 4:
			ThunderLights(1,0.05f);
		break;
		case 5:
			ThunderLights(2,0.05f);
		break;
		case 6:
			ThunderLights(3,0.05f);
		break;
		case 7:
			ThunderLights(1,0.3f);
			PlaySoundAtEntity("Thunder", "general_thunder.snt", "AreaThunder", 0.0f, false);
		break;
	}
	
	if(GetLocalVarInt("ThunderStep") < 8)  AddTimer("thunder", fEventSpeed, "TimerThunder");
	else { 
		SetLocalVarInt("ThunderStep", 0); 
		
		AddTimer("thunder", RandFloat(10.0f, 30.0f), "TimerThunder"); 
	}
}
void ThunderLights(int alIntensity, float afFade)
{
	/*Skip parts of a flash everynow and then but not the first strong light 
	**and not the last "back to normal" light
	 */
	if(RandFloat(1, 3) == 1 && (GetLocalVarInt("ThunderStep") != 3 or GetLocalVarInt("ThunderStep") != 7)) return;
	
	float fF = 0.2f;
	
	switch(alIntensity) {
		case 1:
			for(int i=1;i<=5;i++) FadeLightTo("spotthunder_"+i,0.52f,0.55f,0.6f,0.45f,-1,afFade-0.04f);
			for(int i=1;i<=7;i++) FadeLightTo("pointthunder_"+i,0.32f,0.35f,0.4f,0.2f,-1,afFade-0.025f);
			for(int i=1;i<=3;i++) FadeLightTo("ambthunder_"+i,0.2f,0.25f,0.35f,-1,-1,afFade);
		break;
		case 2:
			for(int i=1;i<=5;i++) FadeLightTo("spotthunder_"+i,0.82f+fF,0.85f+fF,0.9f+fF,0.9f+fF,-1,afFade-0.04f);
			for(int i=1;i<=7;i++) FadeLightTo("pointthunder_"+i,0.72f+fF,0.75f+fF,0.8f+fF,0.4f+fF,-1,afFade-0.025f);
			for(int i=1;i<=3;i++) FadeLightTo("ambthunder_"+i,0.25f+fF,0.3f+fF,0.4f+fF,-1,-1,afFade);
		break;
		case 3:
			for(int i=1;i<=5;i++) FadeLightTo("spotthunder_"+i,0.92f+fF,0.95f+fF,1+fF,1+fF,-1,afFade-0.04f);
			for(int i=1;i<=7;i++) FadeLightTo("pointthunder_"+i,0.82f+fF,0.85f+fF,0.9f+fF,0.5f+fF,-1,afFade-0.025f);
			for(int i=1;i<=3;i++) FadeLightTo("ambthunder_"+i,0.3f+fF,0.35f+fF,0.45f+fF,-1,-1,afFade);
		break;
	}
}
/*End the thunder loop when in hall 3
 */
void CollideAreaThunder(string &in asParent, string &in asChild, int alState)
{
	if(asChild == "areabeginthunder") {
		AddTimer("thunder", RandFloat(1, 5), "TimerThunder"); 
		
		AddEntityCollideCallback("Player", "areaendthunder", "CollideAreaThunder", true, 1);
		
	} else {
		RemoveTimer("thunder");
		
		ThunderLights(1,0.3f);
		
		AddEntityCollideCallback("Player", "areabeginthunder", "CollideAreaThunder", true, 1);
	}
}

//---------------------------------------------

//////////////////////////////////////
// BEGIN NARROW HALLS
//////////////////////////////////////

//---------------------------------------------

/*Begin events in narrow hallway
 */
void CollideNarrowHalls(string &in asParent, string &in asChild, int alState)
{
	if(alState==1){
		PlaySoundAtEntity("creak", "00_creak.snt", "Player", 3, false);
		
		for(int i=1;i<=9;i++) CreateParticleSystemAtEntity("AreaHallPS_"+i, "ps_dust_falling_narrow", "AreaHallPS_"+i, false);

		FadePlayerAspectMulTo(1.7f, 0.03f); 
		
		FadeImageTrailTo(1.5f,1.25f);
		
		AddTimer("hallway", RandFloat(0.1f,1.5f), "HallwayEvents");
		RemoveTimer("lookloop");
		StopRandomLook();
		
		if(GetLocalVarInt("HallwaySteps") == 0)
			AddTimer("steps", 0.5f, "HallwaySteps");
		
	} else {
		StopSound("creak", 3);
		
		for(int i=1;i<=9;i++) DestroyParticleSystem("AreaHallPS_"+i);
		
		SetPlayerMoveSpeedMul(1);
			
		FadePlayerAspectMulTo(1,0.3f); 
		
		FadeImageTrailTo(0.0f,1.5f);
		
		AddTimer("lookloop", RandFloat(2.0f,6.0f), "TimerRandomLook");
		RemoveTimer("hallway");
		
		SetLocalVarInt("HallwayStep", 0); 
		SetLocalVarInt("InitUniqueRandom", 0);
	}
}
/*Footsteps that run away when entering hallway
 */
float fStep=0.0f;
float fLoop=0.4f;
void HallwaySteps(string &in asTimer)
{	
	AddLocalVarInt("HallwaySteps", 1);
	
	if(GetLocalVarInt("HallwaySteps") <= 5) {
		PlaySoundAtEntity("step", "scare_walk_hallway.snt", "Arealookhall3", fStep+=0.03f, false);
		AddTimer("steps", fLoop-=0.04f , "HallwaySteps");
		
	} else if(GetLocalVarInt("HallwaySteps") >= 6 && GetLocalVarInt("HallwaySteps") <= 10) {
		PlaySoundAtEntity("step", "scare_walk_hallway.snt", "Arealookhall3", fStep+=0.01f, false);
		AddTimer("steps", fLoop+=0.1f , "HallwaySteps");
	}
	
	AddDebugMessage("Step!" + fStep, false);	
}
/*Random events before passing out, swinging candeliers
 */
bool bSwing = true;
void HallwayEvents(string &in asTimer)
{	
	SetLocalVarInt("HallwayStep", RandFloat(1,4));	//What step to play in the event
	
	float fEventSpeed = 2.0f;		//The default time between steps in an event
	
	bSwing = bSwing == false ? true : false;

	if(bSwing){
		for(int i=0;i<=110;i++) {
			AddBodyForce("chand1_Body_3", i*0.04f, 0, i*0.04f, "World"); 
			AddBodyForce("chand2_Body_3", i*0.04f, 0, i*0.04f, "World");
			AddBodyForce("chandelier_simple_3_Body_3", i*0.04f, 0, i*0.04f, "World");
		}
	
	} else {
		for(int i=0;i<=110;i++) {
			AddBodyForce("chand1_Body_3", -i*0.04f, 0, -i*0.04f, "World"); 
			AddBodyForce("chand2_Body_3", -i*0.04f, 0, -i*0.04f, "World");
			AddBodyForce("chandelier_simple_3_Body_3", -i*0.04f, 0, -i*0.04f, "World");
		}
	}
		
	switch(GetLocalVarInt("HallwayStep")){
		case 1:
			PlaySoundAtEntity("breath", "react_breath.snt", "Player", 1.0 / 0.3f,false);
			SetPlayerMoveSpeedMul(0.85f);
			StartScreenShake(0.002f,1, 0.5f,0.5f);
		break;
		case 2:
			PlaySoundAtEntity("breath", "react_breath.snt", "Player", 1.0 /  0.4f,false);
			SetPlayerMoveSpeedMul(0.8f);
			StartScreenShake(0.004f,1, 0.5f,0.5f);
		break;
		case 3:
			PlaySoundAtEntity("breath", "react_breath.snt", "Player", 1.0 / 0.7f,false);
			SetPlayerMoveSpeedMul(0.75f);
			StartScreenShake(0.005f,1, 0.5f,0.5f);
		break;
		case 4:
			PlaySoundAtEntity("breath", "react_breath.snt", "Player", 1.0 / 0.5f,false);
			SetPlayerMoveSpeedMul(0.7f);
			StartScreenShake(0.006f,1, 0.5f,0.5f);
		break;
	}
	
	 AddTimer("hallway", fEventSpeed, "HallwayEvents");
}
/*In middle of passage, super scary event occurs and players passes out
 */
void CollideAreaNarrowPassOut(string &in asParent, string &in asChild, int alState)
{
	SetEntityActive("areanarrowhalls", false);	//Turn of the area that triggers the hallway spooks
	
	StopRandomLook();	//De-activate the spinning head
	
	SetPlayerJumpDisabled(true);
	SetPlayerCrouchDisabled(true);

	PlaySoundAtEntity("scare", "react_scare.snt", "Player", 1.0 / 1,false);
	//PlaySoundAtEntity("cuts", "00_cuts.snt", "Player", 1.0 / 4,false);
	PlaySoundAtEntity("cuts", "00_cuts_3d.snt", "Arealookhall3_1", 1.0 / 4,false);
	StopSound("creak", 3);
	
	StartScreenShake(0.01f,0.45f, 0.1f,0.75f);
	
	FadeImageTrailTo(1.7f,1.0f);
	FadePlayerFOVMulTo(1.5f, 0.5f);
	
	AddTimer("narrowpass", 3, "NarrowPassOutEvents");

	/*Turn off hallway events
	 */
	FadePlayerAspectMulTo(1,0.1f); 
	RemoveTimer("hallway");
}
void NarrowPassOutEvents(string &in asTimer)
{
	AddLocalVarInt("HallPassOutStep", 1);	//What step to play in the event
	float fEventSpeed = 0.5f;		//The default time between steps in an event
	
	switch(GetLocalVarInt("HallPassOutStep")){
		case 1:
			PlayMusic("00_event_hallway", false, 1,1, 10, false);
			PlaySoundAtEntity("scare", "react_scare.snt", "Player", 0,false);
			StartPlayerLookAt("Arealookhall1", 0.5f, 0.5f, "");
			SetPlayerMoveSpeedMul(0.7f);
			SetPlayerLookSpeedMul(0.7f);
			fEventSpeed = 1.0f;
		break;
		case 2:
			PlaySoundAtEntity("loop", "00_loop.snt", "Player", 1.0 / 0.05f, false);
			SetPlayerMoveSpeedMul(0.6f);
			SetPlayerLookSpeedMul(0.6f);
			SetPlayerCrouching(false);
			MovePlayerHeadPos(0, -1.3f, 0, 0.75, 0.5f);
			PlaySoundAtEntity("movement", "player_climb.snt", "Player", 0,false);
		break;
		case 3:
			PlaySoundAtEntity("scare", "react_scare.snt", "Player", 1.0 / 0.5f,false);
			SetPlayerMoveSpeedMul(0.5f);
			SetPlayerLookSpeedMul(0.5f);
			FadePlayerRollTo(30, 2, 10);
		break;
		case 4:
			FadeImageTrailTo(0.0f,0.5f);
			SetPlayerMoveSpeedMul(0.8f);
			SetPlayerLookSpeedMul(0.8f);
			FadePlayerFOVMulTo(1, 1);
			MovePlayerHeadPos(0, 0, 0, 2, 0.5f);
			fEventSpeed = 1.0f;
		break;
		case 5:
			SetPlayerMoveSpeedMul(0.4f);
			SetPlayerLookSpeedMul(0.4f);
			StartPlayerLookAt("Arealookhall2", 1, 1, "");
			MovePlayerHeadPos(0, -1.3f, 0, 5, 0.25f);
			//PlaySoundAtEntity("bodyfall", "player_bodyfall.snt", "Player", 0,false);
			PlaySoundAtEntity("movement", "player_climb.snt", "Player", 0,false);
			PlaySoundAtEntity("breath", "react_breath.snt", "Player", 1.0 / 0.75f,false);
			fEventSpeed = 1.0f;
		break;
		case 6:
			PlaySoundAtEntity("scare", "react_scare.snt", "Player", 1.0 / 1,false);
			SetPlayerMoveSpeedMul(0.3f);
			SetPlayerLookSpeedMul(0.3f);
			FadePlayerRollTo(-30, 10, 20);
		break;
		case 7:
			SetPlayerMoveSpeedMul(0.6f);
			SetPlayerLookSpeedMul(0.6f);
			StartPlayerLookAt("Arealookhall1", 2, 2, "");
			FadePlayerRollTo(30, 20, 40);
			MovePlayerHeadPos(-1, -1.3f, 0, 1, 0.25f);
			//StopSound("Eerie", 0.25f);
			//StopSound("Hollow", 0.25f);
			PlaySoundAtEntity("movement", "player_climb.snt", "Player", 0,false);
		break;
		case 8:
			SetPlayerMoveSpeedMul(0.4f);
			SetPlayerLookSpeedMul(0.4f);
			FadeOut(3);
			FadePlayerRollTo(75, 15, 30);
			StopPlayerLookAt();
		break;
		case 9:
			for(int i=1;i<=9;i++) DestroyParticleSystem("AreaHallPS_"+i);
			GiveSanityDamage(10, false);
			PlaySoundAtEntity("faint", "00_faint.snt", "Player", 1.0 / 1,false);
			SetPlayerMoveSpeedMul(0.2f);
			SetPlayerLookSpeedMul(0.2f);
			//StopPlayerLookAt();
			//AddTimer("inactive", 1.0f, "TimerPlayerInactive");
			AddTimer("narrowpass2", 0.1f, "NarrowPassOutEvents02");
			SetLocalVarInt("HallPassOutStep", 0);
			return;
		break;
	}
	
	if(GetLocalVarInt("HallPassOutStep") < 10)   AddTimer("narrowpass", fEventSpeed, "NarrowPassOutEvents");
}
/*Player wakes up again and can continue onwards
 */
void NarrowPassOutEvents02(string &in asTimer)
{
	AddLocalVarInt("HallPassOutStep", 1);	//What step to play in the event
	float fEventSpeed = 0.5f;		//The default time between steps in an event
	
	switch(GetLocalVarInt("HallPassOutStep")){
		case 1:
			//SetPlayerActive(true);
			FadeImageTrailTo(1.8f,2.0f);
			//PlaySoundAtEntity("Pant!", "react_pant.snt", "Player", 0,false);
			StopSound("loop", 3);
			FadeIn(1);
			FadePlayerRollTo(30, 2, 10);
		break;
		case 2:
			//PlaySoundAtEntity("Eerie", "ambience_wind_eerie_no3d.snt", "AreaThunder", 1.0 / 0.1f,false);
			//PlaySoundAtEntity("Hollow", "ambience_hollow_tinker.snt", "AreaThunder", 1.0 / 0.1f, false);
			StartPlayerLookAt("Arealookhall1", 0.1f, 0.1f, "");
			MovePlayerHeadPos(0, -1.0f, 0, 0.5, 0.5f);
			PlaySoundAtEntity("movement", "player_climb.snt", "Player", 0, false);
		break;
		case 3:
			SetPlayerLookSpeedMul(0.3f);
			SetPlayerMoveSpeedMul(0.3f);
		break;
		case 4:
			SetPlayerLookSpeedMul(0.4f);
			SetPlayerMoveSpeedMul(0.4f);
			MovePlayerHeadPos(0, 0, 0, 2, 0.5f);
		break;
		case 5:
			SetPlayerLookSpeedMul(0.5f);
			SetPlayerMoveSpeedMul(0.5f);
			MovePlayerHeadPos(0, -1.3f, 0, 3, 0.5f);
			PlaySoundAtEntity("bodyfall", "player_bodyfall.snt", "Player", 1.0 / 1, false);
			PlaySoundAtEntity("movement", "player_climb.snt", "Player", 0, false);
			StartPlayerLookAt("Arealookhall3", 1.8f, 1.8f, "");
			FadePlayerRollTo(-30, 3, 10);
		break;
		case 6:
			SetPlayerLookSpeedMul(0.6f);
			SetPlayerMoveSpeedMul(0.7f);
			FadePlayerRollTo(0, 14, 35);
		break;
		case 7:
			SetPlayerLookSpeedMul(0.7f);
			SetPlayerMoveSpeedMul(0.7f);
			StartPlayerLookAt("Arealookhall1", 5.1f, 5.1f, "");
			
		break;
		case 8:
			SetPlayerLookSpeedMul(0.8f);
			SetPlayerMoveSpeedMul(0.8f);
			MovePlayerHeadPos(0, 0, 0, 4, 0.5f);
			PlaySoundAtEntity("movement", "player_climb.snt", "Player", 0, false);
			StartPlayerLookAt("Arealookhall3", 5.1f, 5.1f, "");
		break;
		case 9:
			SetPlayerLookSpeedMul(0.9f);
			SetPlayerMoveSpeedMul(0.9f);
			FadeImageTrailTo(0,0.2f);
			StopPlayerLookAt();
		break;
		case 10:
			SetPlayerLookSpeedMul(1.0f);
			SetPlayerMoveSpeedMul(1.0f);
			PlaySoundAtEntity("breath", "react_breath.snt", "Player", 1.0 / 1, false);
			fEventSpeed = 2.0f;
		break;
		case 11:
			//SetPlayerLookSpeedMul(0.9f);
			//SetPlayerMoveSpeedMul(0.9f);
			AddTimer("lookloop", RandFloat(3.0f,6.0f), "TimerRandomLook");	//Re-activate the spinning head
			PlaySoundAtEntity("breath", "react_breath.snt", "Player", 1.0 / 0.75f, false);
			fEventSpeed = 2.0f;
		break;
		case 12:
			//SetPlayerLookSpeedMul(1.0f);
			//SetPlayerMoveSpeedMul(1.0f);
			SetPlayerJumpDisabled(false);
			SetPlayerCrouchDisabled(false);
			PlaySoundAtEntity("breath", "react_breath.snt", "Player", 1.0 / 0.5f, false);
			fEventSpeed = 2.0f;
		break;
	}
	if(GetLocalVarInt("HallPassOutStep") < 13)   AddTimer("narrowpass2", fEventSpeed, "NarrowPassOutEvents02");
}
void TimerPlayerInactive(string &in asTimer)
{
	SetPlayerActive(false);
}

//---------------------------------------------

///////////////////////////////////
// BEGIN GALLERY DOOR SWING OPEN
///////////////////////////////////

//---------------------------------------------

/*When in hall4 gallery door swings open slowly
 */
void CollideAreaGalleryDoor(string &in asParent, string &in asChild, int alState)
{
	SetSwingDoorClosed("door_gallery", false, false);
	SetSwingDoorDisableAutoClose("door_gallery", true);
	
	PlaySoundAtEntity("creaking_door", "joint_door_move_special.snt", "door_gallery", 1.0 / 0.7f, false);
	
	AddTimer("door_gallery", 0.01f, "TimerSwingDoor");
	
	AddDebugMessage("Boho, the gallery door creaks open.", false);	
	
	AddTimer("stopcreak", 2.0f, "TimerStopCreak");
}
void TimerSwingDoor(string &in asTimer)
{
	if(GetLocalVarInt("SwingDoor") == 10){
		SetLocalVarInt("SwingDoor", 0);
		return;
	}
	
	if(asTimer == "door_gallery") AddPropForce(asTimer, 70.0f, 0, 0, "World"); 
	else AddPropForce(asTimer, -95.0f, 0, 0, "World"); 
	
	AddLocalVarInt("SwingDoor", 1);
	
	AddTimer(asTimer, 0.03f, "TimerSwingDoor");
	
	AddDebugMessage("Swing: "+GetLocalVarInt("SwingDoor"), false);
}
void TimerStopCreak(string &in asTimer)
{
	if(asTimer == "scare2"){
		PlaySoundAtEntity("scare", "react_scare.snt", "Player", 1.0 / 1, false);
		return;
	}
	if(asTimer == "scare"){
		PlaySoundAtEntity("scare", "react_scare.snt", "Player", 0, false);
		//PlaySoundAtEntity("laugh", "00_laugh.snt", "door_gallery", 1.0 / 1, false);
		PlaySoundAtEntity("loop", "00_loop.snt", "Player", 1.0 / 0.1f, false);
		return;
	}
	if(asTimer == "breath1"){
		PlaySoundAtEntity("breath", "react_breath.snt", "Player", 1.0 / 1, false);
		return;
	}
	if(asTimer == "breath2"){
		PlaySoundAtEntity("breath", "react_breath.snt", "Player", 1.0 / 0.9f, false);
		return;
	}
	if(asTimer == "breath3"){
		//SetSwingDoorLocked("door_gallery", false, true);
		PlaySoundAtEntity("breath", "react_breath.snt", "Player", 1.0 / 0.8f, false);
		AddTimer("lookloop", RandFloat(2.0f,3.0f), "TimerRandomLook");	//Re-activate the spinning head
		FadePlayerFOVMulTo(1.0f, 0.1f);
		StopSound("loop", 1);
		StartScreenShake(0.01f,0.25f, 0.01f,0.5f);
		return;
	}
	if(asTimer == "lightsout"){
		SetLampLit("chandelier_nice_1", false, true);
		SetLampLit("candlestick_floor_2", false, true);
		FadeLightTo("PointLight_25", 0, 0, 0, 0, 0, 1);
		FadePlayerFOVMulTo(1.5f, 0.25f);
		return;
	}
	
	if(asTimer == "sanity"){
		//SetSwingDoorLocked("door_gallery", true, false);
		GiveSanityDamage(10, true);
		return;
	}
	
	StopSound("creaking_door", 0.4f);
}
/*When in the gallery the lights go out and the door slams shut
**When leaving the gallery the door slams shut and locks behind the player.
 */
void CollideAreaGallery(string &in asParent, string &in asChild, int alState)
{
	if(asChild == "gallery_2"){
		//if(!GetSwingDoorClosed("door_gallery")) 
			//PlaySoundAtEntity("slam_door", "scare_slam_door.snt", "door_gallery", 0, false);
		//SetSwingDoorLocked("door_gallery", true, true);
		//PlaySoundAtEntity("easter?", "scare_breath.snt", "door_gallery", 1.0 / 0.5f, false);
		//AddTimer("scare2", 0.1f, "TimerStopCreak");
		return;
	}
	
	UnBlockHint("DarknessDecrease"); 
	
	StopRandomLook();	//De-activate the spinning head
	
	//if(!GetSwingDoorClosed("door_gallery")) 
		//PlaySoundAtEntity("slam_door", "scare_slam_door.snt", "door_gallery", 0, false);
		
	//SetSwingDoorClosed("door_gallery", true, true);
	//SetSwingDoorDisableAutoClose("door_gallery", false);
	
	PlayMusic("00_event_gallery", false, 1,1, 10, false);
	PlaySoundAtEntity("windgal", "general_wind_whirl.snt", "gallery_1", 0.0f, false);
	CreateParticleSystemAtEntity("psgal", "ps_dust_whirl.ps", "gallery_1", false);
	
	StartScreenShake(0.01f,0.25f, 0.01f,0.5f);
	
	AddEntityCollideCallback("Player", "gallery_2", "CollideAreaGallery", true, 1);
	
	AddTimer("sanity", 0.7f, "TimerStopCreak");
	AddTimer("lightsout", 0.8f, "TimerStopCreak");
	AddTimer("scare", 1.2f, "TimerStopCreak");
	AddTimer("breath1", 2.0f, "TimerStopCreak");
	AddTimer("breath2", 5.0f, "TimerStopCreak");
	AddTimer("breath3", 8.0f, "TimerStopCreak");
}

//---------------------------------------------	
	
////////////////////////
//GUST DOOR OPEN
////////////////////////

//---------------------------------------------

/*When entering hall 3 the door to the couch room is opened by a gust of wind
 */
void CollideAreaGustDoor(string &in asParent, string &in asChild, int alState)
{
	StartScreenShake(0.003f,0.25f, 1,1);
	
	PlaySoundAtEntity("gust_door", "general_wind_whirl.snt", "door_gust", 1.0 / 3, false);
	
	CreateParticleSystemAtEntity("gust", "ps_dust_push", "AreaDust_4", false);
	
	SetSwingDoorClosed("door_gust", false, false);
	SetSwingDoorDisableAutoClose("door_gust", true);
	
	AddTimer("gusttimer", 0.4f, "TimerGustDoor");
	
	AddDebugMessage("Uhm, the door gust opened?", false);	
}
void TimerGustDoor(string &in asTimer)
{
	PlaySoundAtEntity("scare", "react_scare.snt", "Player", 0, false);
	PlaySoundAtEntity("creaking_door", "joint_door_move_special.snt", "door_gust", 1.0 / 1.8f, false);
	
	GiveSanityDamage(5, true);
	
	AddTimer("breath1", 2.0f, "TimerStopCreak");
	AddTimer("breath2", 5.0f, "TimerStopCreak");
	AddTimer("breath3", 8.0f, "TimerStopCreak");
	
	AddTimer("door_gust", 0.3f, "TimerSwingDoor");
	
	StopSound("creaking_door", 0.4f);
}
//END GUST DOOR OPEN//
//////////////////////

//---------------------------------------------	

/////////////////////////////////
//BEGIN GUST FLOWS//
/////////////////////////////////

//---------------------------------------------	

/*Some gusts will appear in the early parts of the level and one at the very end
 */
void CollideAreaGust(string &in asParent, string &in asChild, int alState)
{
	if(asChild == "AreaDust1") {
		int makeyourmagicpick = RandFloat(2,3);
		if(makeyourmagicpick == 2) AddEntityCollideCallback("Player", "AreaDust2", "CollideAreaGust", true, 1);
		else AddEntityCollideCallback("Player", "AreaDust3", "CollideAreaGust", true, 1);
	}
	
	if(asChild == "AreaDustEnd"){ 
		CreateParticleSystemAtEntity(asChild, "ps_dust_whirl", asChild+"_ps", false);
		UnBlockHint("DarknessDecrease");
	}
	else CreateParticleSystemAtEntity(asChild, "ps_dust_push", asChild+"_ps", false);
	
	PlaySoundAtEntity(asChild+"s", "general_wind_whirl.snt", asChild+"_ps", 1.0 / 0.5f, false);
}

//---------------------------------------------	

/////////////////////////////
// PLAYER ALMOST FAINT
/////////////////////////////

//---------------------------------------------	

void CollideAreaFaint(string &in asParent, string &in asChild, int alState)
{
	StopRandomLook();	//De-activate the spinning head
	
	SetPlayerJumpDisabled(true);
	SetPlayerCrouchDisabled(true);
	
	AddTimer("TimerFaintEvents", 0.5f, "TimerFaintEvents");
	
	FadePlayerFOVMulTo(1.5f, 0.25f);
	
	AddDebugMessage("Oh my, I think I'm almost going to pass out, almost...", false);
}
void TimerFaintEvents(string &in asTimer)
{	
	/*Configurables
	 */
	int iMaxEventStep = 8;		//How many steps there are in the switch event
	float fEventSpeed = 1.0f;	//The default time between steps in an event
	
	/*Helpers - Do not edit
	 */
	string sEvent = asTimer;	//Using first timer name for variable, timer name & callback for the timer that loops
	AddLocalVarInt(sEvent, 1);	//What step to play in the event
	
	switch(GetLocalVarInt(sEvent)){
		case 1:
			StartPlayerLookAt("Areafaintlook_1", 0.1f, 0.1f, "");
			SetPlayerMoveSpeedMul(0.85f);
			StartScreenShake(0.002f,1, 0.5f,0.5f);
			PlaySoundAtEntity("loop", "00_loop.snt", "Player", 1.0 / 0.1f, false);
			PlaySoundAtEntity("faint", "00_faint.snt", "Player", 1.0 / 0.08f, false);
		break;
		case 2:
			MovePlayerHeadPos(-0.25f, -0.5f, 0, 1, 0.5f);
			PlaySoundAtEntity("movement", "player_climb.snt", "Player", 0, false);
			FadePlayerRollTo(-50, 3, 5);
			SetPlayerMoveSpeedMul(0.75f);
			StartScreenShake(0.004f,1, 0.5f,0.5f);
		break;
		case 3:
			FadeOut(3);
			MovePlayerHeadPos(0.25f, -0.3f, 0, 1, 0.5f);
			StartPlayerLookAt("Areafaintlook_2", 0.3f, 0.3f, "");
			SetPlayerMoveSpeedMul(0.7f);
			StartScreenShake(0.005f,1, 0.5f,0.5f);
		break;
		case 4:
			FadePlayerRollTo(50, 5, 5);
			SetPlayerMoveSpeedMul(0.5f);
			StartScreenShake(0.006f,1, 0.5f,0.5f);
			PlaySoundAtEntity("bodyfall", "player_bodyfall.snt", "Player", 1.0 / 0.3f, false);
		break;
		case 5:
			FadeIn(1.5f);
			MovePlayerHeadPos(0, -0.8f, 0, 0.75f, 0.5f);
			PlaySoundAtEntity("movement", "player_climb.snt", "Player", 0, false);
			StartPlayerLookAt("Areafaintlook_2", 0.1f, 0.1f, "");
			SetPlayerMoveSpeedMul(0.3f);
		break;
		case 6:
			StopSound("faint", 0.2f);
			FadeOut(3);
			FadePlayerRollTo(-25, 3, 5);
			SetPlayerMoveSpeedMul(0.4f);
		break;
		case 7:
			StartPlayerLookAt("Areafaintlook_1", 0.2f, 0.2f, "");
			SetPlayerMoveSpeedMul(0.7f);
			FadePlayerFOVMulTo(1.0f, 0.1f);
		break;
		case 8:
			StopSound("loop", 0.2f);
			FadeIn(1.5f);
			MovePlayerHeadPos(0, 0, 0, 1, 0.5f);
			PlaySoundAtEntity("movement", "player_climb.snt", "Player", 0, false);
			FadePlayerRollTo(0, 2, 4);
			StopPlayerLookAt();
			SetPlayerMoveSpeedMul(1);
			AddTimer("lookloop", RandFloat(2.0f,6.0f), "TimerRandomLook");	//Re-activate the spinning head
			
			// Reset player input
			SetPlayerJumpDisabled(false);
			SetPlayerCrouchDisabled(false);
		break;
	}
	
	if(GetLocalVarInt(sEvent) <= iMaxEventStep) AddTimer(sEvent, fEventSpeed, sEvent);
}

//---------------------------------------------	

//////////////////////////
// RANDOM LOOK SPIN
//////////////////////////

//---------------------------------------------	

/*Player has a bit of a random head during the level
 */
bool bRoll = true;
void TimerRandomLook(string &in asTimer)
{
	int iLook = RandFloat(1,5);
	
	if(iLook > 4){
		SetPlayerRunSpeedMul(0.5);
		SetPlayerMoveSpeedMul(1);
		FadePlayerRollTo(0, 0.5f, 1); 
		FadePlayerFOVMulTo(1, 1);
		FadeImageTrailTo(0,1.5f);
		
		AddTimer("lookloop", RandFloat(6.0f,8.0f), "TimerRandomLook");
		return;
	}
	
	if(iLook == 1 or iLook == 3)
		PlaySoundAtEntity("sigh", "react_sigh.snt", "Player", 1.0 / 0.75f, false);
	
	FadePlayerFOVMulTo(RandFloat(0.7f,1.3f), RandFloat(0.05f,0.1f));
	
	SetPlayerMoveSpeedMul(RandFloat(0.75f,0.9f));
	SetPlayerRunSpeedMul(RandFloat(0.4f,0.55f));
	
	FadeImageTrailTo(RandFloat(0.75f,1.0f),RandFloat(0.75f,1.25f));
	
	bRoll = bRoll == false ? true : false;
	
	if(bRoll)
		FadePlayerRollTo(RandFloat(4,12), RandFloat(0.075f,0.35f), RandFloat(0.55f,1.15f)); 
	else
		FadePlayerRollTo(RandFloat(-4,-12), RandFloat(0.05f,0.25f), RandFloat(0.5f,1)); 
		
	AddTimer("lookloop", RandFloat(3.0f,6.0f), "TimerRandomLook");
}
void StopRandomLook()
{
	RemoveTimer("lookloop");
	
	SetPlayerMoveSpeedMul(1);
	FadePlayerFOVMulTo(1, 1);
	FadeImageTrailTo(0,1.0f);
	FadePlayerRollTo(0, 0.5f, 2); 
}

//---------------------------------------------	

/////////////////////////////
// INTERACT LARGE GATE
/////////////////////////////

//---------------------------------------------	

void InteractLargeGate(string &in asEntity)
{
	PlaySoundAtEntity("guardboo", "guardian_distant1", "AreaLargeGate", 3.0f, false);
	PlaySoundAtEntity("thunderboo", "general_thunder.snt", "AreaLargeGate", 0.0f, false);
	
	PlayGuiSound("close_gate.ogg", 0.4f);
	
	AddTimer("1", 0.4f, "TimerLargeGate");
	AddTimer("2", 0.1f, "TimerLargeGate");
	AddTimer("3", 0.6f, "TimerLargeGate");
	AddTimer("4", 2.0f, "TimerLargeGate");
}
void TimerLargeGate(string &in asTimer)
{
	if(asTimer == "1"){
		PlayMusic("01_event_critters", false, 0.7f, 1.0f, 10, false);
		StartScreenShake(0.005f, 2.0f, 1.0f, 1.0f);
		GiveSanityDamage(10.0f, true);
	}
	else if(asTimer == "2"){
		
	}
	else if(asTimer == "3"){
		PlayGuiSound("react_scare", 0.5f);
	}
	else if(asTimer == "4"){
		StopMusic(6.0f, 10);
		StopSound("guardboo", 2.0f);
		StopSound("thunderboo", 4.0f);
		
		SetEntityPlayerInteractCallback("LargeGate", "InteractLargeGate02", true);
	}
}
void InteractLargeGate02(string &in asEntity)
{
	
}

//---------------------------------------------	

/////////////////////////////
// MISC
/////////////////////////////

//---------------------------------------------	


void LookAtQuest(string &in asEntity, int alState)
{
	if(QuestIsAdded("00Trail") == false && QuestIsCompleted("00Trail") == false)
	{
		AddQuest("00Trail", "00Trail");
		
		if(ScriptDebugOn() == false)
		{
			AutoSave();
		}
	}
}
void CollideAreaQuest(string &in asParent, string &in asChild, int alState)
{
	LookAtQuest("", 0);
}

//////////////////////
////////////////////////////
// Run first time starting map
void OnStart()
{
	SetMapDisplayNameEntry("RainyHall");
	
	//----COLLIDE CALLBACKS----//
	AddEntityCollideCallback("Player", "areanarrowhalls", "CollideNarrowHalls", false, 0);		//Begin/End narrowhall effects
	AddEntityCollideCallback("Player", "areaendthunder", "CollideAreaThunder", true, 1);		//Begin/End thunder loop
	AddEntityCollideCallback("Player", "narrowpassout", "CollideAreaNarrowPassOut", true, 1);	//Player passes out in hallway
	AddEntityCollideCallback("Player", "gallerydoor", "CollideAreaGalleryDoor", true, 1);	//Gallery door creaks open
	AddEntityCollideCallback("Player", "gallery", "CollideAreaGallery", true, 1);			//Gallery door slam behind player
	AddEntityCollideCallback("Player", "areagustdoor", "CollideAreaGustDoor", true, 1);		//Gust swings door open in hall 3
	AddEntityCollideCallback("Player", "AreaDust1", "CollideAreaGust", true, -1);		//Area that starts a gust blow in hallX
	AddEntityCollideCallback("Player", "AreaDustEnd", "CollideAreaGust", true, -1);		//Area that starts a gust just at the end
	AddEntityCollideCallback("Player", "AreaFaint", "CollideAreaFaint", true, 1);		//Player almost faints in hall 2
	AddEntityCollideCallback("Player", "AreaQuest", "CollideAreaQuest", true, -1);		//Player almost faints in hall 2

	//---- VARIABLES ----//

	
	//---- INTRO SETTINGS ----//

	
	//FadePlayerFOVMulTo(2, 1);	//2 == max? > 1 är fisheye.	fov,speed
	//FadePlayerAspectMulTo(2, 0.1); // > 1 är compressed vy. aspect, speed
	if(ScriptDebugOn() == false)
	{
		FadeGlobalSoundVolume(0, 0.01f);
		FadeGlobalSoundSpeed(0.7, 0.01f);
		SetPlayerActive(false);
		ShowPlayerCrossHairIcons(false);
		SetInventoryDisabled(true);
		SetPlayerJumpDisabled(true);
		SetPlayerCrouchDisabled(true);
		FadeOut(0);
		
		//disable some event areas
		SetEntityActive("narrowpassout", false);
		SetEntityActive("gallerydoor", false);
		SetEntityActive("areanarrowhalls", false);
		SetEntityActive("areaendthunder", false);
		SetEntityActive("areabeginthunder", false);
		SetEntityActive("areagustdoor", false);
		SetEntityActive("AreaDust3", false);
		SetEntityActive("AreaFaint", false);
		
		//Teleport player to first start pos
		TeleportPlayer("IntroStart_1");
	
		AddTimer("StartGame", 1, "TimerStartGame");
	}
	
	//----QUEST INIT----//
	SetPlayerLampOil(0);	//This is so that the player has 0 lampoil when find the lantern in level 1.
	SetNumberOfQuestsInMap(0);	
	
	BlockHint("SanityAdd"); BlockHint("LanternNoItem"); BlockHint("LanternNoOil"); BlockHint("LeanHint");
	BlockHint("PushHint"); BlockHint("ThrowHint"); BlockHint("EntityGrab02"); BlockHint("EntityPush");
	BlockHint("EntitySlide"); BlockHint("EntityLever"); BlockHint("DarknessDecrease"); BlockHint("SanityHit");

	//SetPlayerSanity(70);
	
	//----INSANITY----//
	SetInsanitySetEnabled("Ch02", false); 
	SetInsanitySetEnabled("Ch03", false);

	
	//----DEBUG----//
	if(ScriptDebugOn())
	{
		AddDebugMessage("Intro disabled as debug is active.", false);
		
		//GiveItemFromFile("lantern", "lantern.ent");
		
		//LEVEL TESTING
		AddTimer("lookloop", RandFloat(2.0f,6.0f), "TimerRandomLook");
		AddTimer("thunder", 1, "TimerThunder");
		SetPlayerRunSpeedMul(0);
	}
}

////////////////////////////
// Run when entering map
void OnEnter()
{	
	//----PRELOADING----//
	PreloadSound("react_sigh"); PreloadSound("react_breath"); PreloadSound("player_climb"); PreloadSound("general_thunder");
	PreloadSound("00_creak"); PreloadSound("scare_walk_hallway"); PreloadSound("00_cuts"); PreloadSound("react_scare");
	PreloadSound("00_loop"); PreloadSound("player_bodyfall"); PreloadSound("00_faint"); PreloadSound("scare_slam_door");
	PreloadSound("ambience_wind_eerie_no3d"); PreloadSound("ambience_hollow_tinker"); PreloadSound("react_pant"); PreloadSound("joint_door_move_special");
	PreloadSound("general_wind_whirl"); PreloadSound("00_laugh"); PreloadSound("general_rain_m"); PreloadSound("03_in_a_bottle.snt"); PreloadSound("quest_added.snt"); PreloadSound("quest_completed.snt"); PreloadSound("unlock_door.snt"); PreloadSound("lock_door.snt");
	
	PreloadParticleSystem("ps_dust_falling_narrow"); PreloadParticleSystem("ps_dust_push"); PreloadParticleSystem("ps_dust_whirl");
	
	SetupCurrentMapBasedOnExtraModes();
}

////////////////////////////
// Run when leaving map
void OnLeave()
{
	//////////////////////
	//Load Screen Setup
	SetupLoadScreen("LoadingText", "Ch01_Beginning", 5, "game_loading_rose.jpg");
}